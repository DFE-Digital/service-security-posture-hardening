[default]
action.email = 0
action.rss = 0 
action.summary_index = 0
auto_summarize = 0
counttype = always
disabled = 0
dispatchAs = owner
dispatch.max_count = 500000
dispatch.time_format = %FT%T.%Q%:z
enableSched = 0 
is_visible = 1
max_concurrent = 1 
run_on_startup = 0
request.ui_dispatch_app = SSPHP 
request.ui_dispatch_view = search 
schedule_as = auto
schedule_priority = default 
schedule_window = auto 
workload_pool = standard_perf 


#########################################################################################################################################################
#########################################################################################################################################################


[ssphp_create_last_ssphp_drop_csv]
cron_schedule = */5 * * * *
dispatch.earliest_time = -30d@d
dispatch.latest_time = now
enableSched = 1
search = index="ssphp_test" sourcetype="azure:security:finding" host="c113p01-splhf04" source="azure_cloud_defender"\
| stats max(SSPHP_RUN) as last_SSPHP_RUN\
| eval ssphp_source="findings"\
\
| append\
    [search index="ssphp_test" sourcetype="azure:security:alert" host="c113p01-splhf04" source="azure_defender_alerts"\
| stats max(SSPHP_RUN) as last_SSPHP_RUN\
| eval ssphp_source="alerts"]\
| outputlookup createinapp=t append=f ssphp_last_ssphp_drop.csv


###############################################################################################################################################################

[ssphp_create_resource_service_map_csv]
cron_schedule = 15 * * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = index="ssphp_test" sourcetype="azure:resource:group"\
       ```[search index="ssphp_test" sourcetype="azure:resource:group" earliest=-3d@d latest=now | stats max(_time) as _time | return _time]```\
| dedup name\
\
| rename name as ssphp_resource_group, location as ssphp_location, "tags.Parent Business" as ssphp_parent_business, tags.Portfolio as ssphp_portfolio, tags.Product as ssphp_product, tags.Service as ssphp_service, "tags.Service Line" as ssphp_service_line, "tags.Service Offering" as ssphp_service_offering, tags.costcenter as ssphp_cost_centre, tags.environment as ssphp_environment\
| eval id=lower(id), ssphp_resource_group=lower(ssphp_resource_group)\
| rex field=id "^\/subscriptions\/(?<ssphp_subscription>[^\/]+)"\
| rex field=ssphp_resource_group "^(?<ssphp_business_service_index>s\d{3})(?<ssphp_environment>p|d|t)[^-]+-(?<ssphp_service_team>[\s\S]*)$"\
| eval ssphp_environment=case(ssphp_environment="t","Test",ssphp_environment="d","Development",ssphp_environment="p","Production",1==1,"Unknown")\
| fields ssphp_subscription, ssphp_resource_group, ssphp_business_service_index, ssphp_environment, ssphp_service_team, ssphp_location, ssphp_parent_business, ssphp_portfolio, ssphp_product, ssphp_service, ssphp_service_line, ssphp_service_offering\
\
| join type=outer ssphp_subscription\
    [search index="ssphp_test" sourcetype="azure:subscriptions" \
    | dedup subscriptionId\
       ```[search index="ssphp_test" sourcetype="azure:subscriptions" earliest=-3d@d latest=now | stats max(_time) as _time | return _time]```\
\
    | rename tenantId as ssphp_tenant, subscriptionId aS ssphp_subscription, displayName as ssphp_subscription_display_name\
    | fields ssphp_subscription, ssphp_tenant, ssphp_subscription_display_name]\
\
| lookup ssphp_business_service_index.csv "Service ID" as ssphp_business_service_index OUTPUT "Cost Centre" as ssphp_cost_centre, "DevOps Site" as ssphp_devops_site, SRO as ssphp_sro, "Service Name" as ssphp_service_name, "Tech Directorate Architect Partner" as ssphp_tech_dir_arch_partner, "Tech Directorate Service Management Contact" as ssphp_tech_dir_serv_mgmt_contact, "Technical Contact Email" as ssphp_tech_contact_email\
\
`ssphp_service_id_logic`\
\
| eval ssphp_service_shortname=case(ssphp_service_name="Regional Service Delivery","RSD",1==1,"Unknown"),\
       ssphp_tenant_name=case(ssphp_tenant="9c7d9dd3-840c-4b3f-818e-552865082e16","CIP",ssphp_tenant="fad277c9-c60a-4da1-b5f3-b3b8b34a82f9","T1",1==1,"SFA : ".substr(ssphp_tenant,0,8))\
\
| fillnull value="Unknown"\
| table ssphp_tenant, ssphp_tenant_name, ssphp_subscription, ssphp_subscription_display_name, ssphp_resource_group, ssphp_business_service_index, ssphp_service_name, ssphp_service_shortname, ssphp_environment, ssphp_service_team, ssphp_location, ssphp_parent_business, ssphp_portfolio, ssphp_product, ssphp_service, ssphp_service_line, ssphp_service_offering, ssphp_cost_centre, ssphp_devops_site, ssphp_sro, ssphp_tech_dir_arch_partner, ssphp_tech_dir_serv_mgmt_contact, ssphp_tech_contact_email\
\
| outputlookup createinapp=t append=f ssphp_resource_service_map.csv


######################################################################################################################################################

[ssphp_create_base_azure_resource_list_csv]
dispatch.earliest_time = -30d@d 
dispatch.latest_time = now 
search = index="eventhub" sourcetype="mscs:azure:eventhub"\
\
| eval resource_id=mvdedup(coalesce(resourceId,ResourceId,id,Id))\
| search resource_id="*/RESOURCEGROUPS/*"\
| dedup resource_id\
| table resource_id\
| rename resource_id as _raw\
| extract pairdelim="/" kvdelim="/"\
| eval ssphp_resource_group=coalesce(resourceGroups,resourcegroups,RESOURCEGROUPS), \
       VIRTUALMACHINES=coalesce(VIRTUALMACHINES,virtualMachines,virtualmachines),\
       ssphp_subscription=coalesce(subscriptions, SUBSCRIPTIONS)\
\
| fields - _raw, activityLogAlerts, providers, PROVIDERS, resourceGroups, resourcegroups, assessments, subassessments, virtualMachines, virtualmachines, RESOURCEGROUPS, _kv, SUBSCRIPTIONS, subscriptions\
\
| rename * as zzz_*, zzz_ssphp_* as ssphp_*\
| foreach zzz_* [eval ssphp_resource=mvappend(ssphp_resource,if(isnotnull('<<FIELD>>'),"<<FIELD>>"." = ".'<<FIELD>>',null()))]\
| fields ssphp_resource_group, ssphp_subscription, ssphp_resource\
| mvexpand ssphp_resource\
| eval ssphp_resource=substr(ssphp_resource,5)\
\
| dedup ssphp_resource_group, ssphp_subscription, ssphp_resource\
\
| outputlookup createinapp=t append=f ssphp_azure_resource_list.csv


#####################################################################################################################################################

[ssphp_create_incremental_azure_resource_list_csv]
cron_schedule = */30 * * * * 
dispatch.earliest_time = -60m@m 
dispatch.latest_time = now 
enableSched = 1 
search = | inputlookup ssphp_azure_resource_list.csv\
\
| append \
    [search index="eventhub" sourcetype="mscs:azure:eventhub" earliest=-1h@h latest=now\
\
| eval resource_id=mvdedup(coalesce(resourceId,ResourceId,id,Id))\
| search resource_id="*/RESOURCEGROUPS/*"\
| dedup resource_id\
| table resource_id\
| rename resource_id as _raw\
| extract pairdelim="/" kvdelim="/"\
| eval ssphp_resource_group=coalesce(resourceGroups,resourcegroups,RESOURCEGROUPS), \
       VIRTUALMACHINES=coalesce(VIRTUALMACHINES,virtualMachines,virtualmachines),\
       ssphp_subscription=coalesce(subscriptions, SUBSCRIPTIONS)\
\
| fields - _raw, activityLogAlerts, providers, PROVIDERS, resourceGroups, resourcegroups, assessments, subassessments, virtualMachines, virtualmachines, RESOURCEGROUPS, _kv, SUBSCRIPTIONS, subscriptions\
\
| rename * as zzz_*, zzz_ssphp_* as ssphp_*\
| foreach zzz_* [eval ssphp_resource=mvappend(ssphp_resource,if(isnotnull('<<FIELD>>'),"<<FIELD>>"." = ".'<<FIELD>>',null()))]\
| fields ssphp_resource_group, ssphp_subscription, ssphp_resource\
| mvexpand ssphp_resource\
| eval ssphp_resource=substr(ssphp_resource,5)]\
\
| dedup ssphp_resource_group, ssphp_subscription, ssphp_resource\
\
| outputlookup createinapp=t append=f ssphp_azure_resource_list.csv