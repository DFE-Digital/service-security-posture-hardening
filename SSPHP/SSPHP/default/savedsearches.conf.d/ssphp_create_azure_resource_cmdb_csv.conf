[ssphp_create_azure_resource_cmdb_csv]
cron_schedule = 25 */12 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
`azure_resource_graph_latest`

| fields - tags.hidden*

`ssphp_azure_add_ip_addresses`

| rex field=type "(microsoft\.)?(?<ssphp_resource_type_category>[^\/]+)\/(?<ssphp_resource_type_type>.*)"

| eval ssphp_type='type',
       ssphp_tenant='tenantId',
       ssphp_tenant_name=case(ssphp_tenant="1a92889b-8ea1-4a16-8132-347814051567","SFA : 1a92889b",
                              ssphp_tenant="274c76d8-64a2-4522-96f1-e6b4e4548bf1","SFA : 274c76d8",
                              ssphp_tenant="3aacc835-4b83-483d-841d-cd787f6f1486","SFA : 3aacc835",
                              ssphp_tenant="9c7d9dd3-840c-4b3f-818e-552865082e16","CIP",
                              ssphp_tenant="aaf44356-4d9d-42b4-85d2-e657d7c716a8","SFA : aaf44356",
                              ssphp_tenant="d2c19871-c122-4c97-9408-e7c6d33e5b52","SFA : d2c19871",
                              ssphp_tenant="f9bc40d7-d249-44e6-9961-ce6d4e3ebf1e","SFA : f9bc40d7",
                              ssphp_tenant="fad277c9-c60a-4da1-b5f3-b3b8b34a82f9","T1",
                              1==1,"Unknown Tenancy"),
       ssphp_subscription='subscriptionId',
       ssphp_resource_group='resourceGroup',
       ssphp_location='location',
       ssphp_name='name',
       ssphp_state='properties.state',
       ssphp_provisioning_state='properties.provisioningState'

| rex field=ssphp_resource_group "^(?<ssphp_business_service_index>s\d{3})"

| join type=outer ssphp_subscription
    [search `azure_index` sourcetype="azure:subscriptions" earliest=-7d latest=now
         [search `azure_index` sourcetype="azure:subscriptions" earliest=-7d latest=now | stats max(SSPHP_RUN) as SSPHP_RUN | return SSPHP_RUN]
    | dedup subscriptionId

    | rename subscriptionId AS ssphp_subscription, displayName AS ssphp_subscription_display_name
    | fields ssphp_subscription, ssphp_subscription_display_name]

| eval ssphp_environment='tags.Environment',
       ssphp_parent_business=coalesce('tags.Parent Business','tags.parent Business'),
       ssphp_portfolio=coalesce('tags.Portfolio','tags.portfolio'),
       ssphp_product=coalesce('tags.Product','tags.product'),
       ssphp_service=coalesce('tags.Service','tags.service'),
       ssphp_service_line=coalesce('tags.Service Line','tags.service line'),
       ssphp_service_offering=coalesce('tags.Service Offering','tags.service Offering'),
       ssphp_environment=coalesce('tags.Environment','tags.Environment','tags.environment'),
       ssphp_contact=coalesce('tags.Contact','tags.contact'),
       ssphp_organisation=coalesce('tags.Organisation','tags.organisation')

| eval ssphp_created_at=mvdedup(mvappend(properties.timeCreated,'properties.created','properties.createdAt','properties.createdAtUtc','properties.createdDate','properties.createdTime','properties.createTime','properties.CreationDate','properties.creationDate','properties.creationTime','properties.issueDate','properties.metadata.createdDateTimeUtc','properties.migrationState.startTimestampUtc','properties.storageProfile.dataDisks{}.createdTime','properties.storageProfile.operatingSystemDisk.createdTime','properties.term.startDate','properties.containers{}.properties.instanceView.currentState.startTime')),
       ssphp_updated_at=mvdedup(mvappend('properties.changedTime','properties.lastModified','properties.timeModified','properties.updatedAt','properties.lastModifiedTime','properties.lastModifiedTimeUtc','properties.metadata.lastUpdatedDateTimeUtc','properties.modifiedDate','properties.policies.retentionPolicy.lastUpdatedTime','properties.policies.softDeletePolicy.lastUpdatedTime','properties.sku.lastSkuUpdate')),
       ssphp_created_at=strftime(strptime(ssphp_created_at,"%Y-%m-%dT%H:%M:%S.%N"),"%Y-%m-%d %H:%M:%S"),
       ssphp_updated_at2=strftime(strptime(ssphp_updated_at,"%Y-%m-%dT%H:%M:%S.%N"),"%Y-%m-%d %H:%M:%S"),
       ssphp_updated_at3=strftime(strptime(ssphp_updated_at,"%a, %d %b %Y %H:%M:%S GMT"),"%Y-%m-%d %H:%M:%S"),
       ssphp_updated_at=coalesce(ssphp_updated_at2,ssphp_updated_at3)
       
| fields - ssphp_updated_at2, ssphp_updated_at3

| lookup ssphp_business_service_index.csv "Service ID" as ssphp_business_service_index OUTPUT "Cost Centre" as ssphp_cost_centre, "DevOps Site" as ssphp_devops_site, SRO as ssphp_sro, "Service Name" as ssphp_service_name, "Tech Directorate Architect Partner" as ssphp_tech_dir_arch_partner, "Tech Directorate Service Management Contact" as ssphp_tech_dir_serv_mgmt_contact, "Technical Contact Email" as ssphp_tech_contact_email

```populate business_service_index and service_name fields```
`ssphp_service_id_logic`

| fields source, ssphp_type, ssphp_id,ssphp_name, ssphp_tenant, ssphp_tenant_name, ssphp_subscription, ssphp_subscription_display_name, ssphp_resource_group, ssphp_resource_type_category, ssphp_resource_type_type, ssphp_state, ssphp_provisioning_state, ssphp_business_service_index, ssphp_level_1, ssphp_level_1_display_name, ssphp_level_2, ssphp_level_2_display_name, ssphp_service_name, ssphp_environment, ssphp_service_team, ssphp_location, ssphp_parent_business, ssphp_portfolio, ssphp_product, ssphp_service, ssphp_service_line, ssphp_service_offering, ssphp_cost_centre, ssphp_devops_site, ssphp_sro, ssphp_tech_dir_arch_partner, ssphp_tech_dir_serv_mgmt_contact, ssphp_tech_contact_email, ssphp_private_ip_address, ssphp_public_ip_address, ssphp_all_ip_addresses, ssphp_created_at, ssphp_updated_at

`ssphp_normalise_service_taxonomy_fields`

| eval SSPHP_RUN=round(now())

| table SSPHP_RUN, source, ssphp_type, ssphp_id, ssphp_name, ssphp_tenant, ssphp_tenant_name, ssphp_subscription, ssphp_subscription_display_name, ssphp_resource_group, ssphp_resource_type_category, ssphp_resource_type_type, ssphp_state, ssphp_provisioning_state, ssphp_business_service_index, ssphp_level_1, ssphp_level_1_display_name, ssphp_level_2, ssphp_level_2_display_name, ssphp_service_name, ssphp_environment, ssphp_service_team, ssphp_location, ssphp_parent_business, ssphp_portfolio, ssphp_product, ssphp_service, ssphp_service_line, ssphp_service_offering, ssphp_cost_centre, ssphp_devops_site, ssphp_sro, ssphp_tech_dir_arch_partner, ssphp_tech_dir_serv_mgmt_contact, ssphp_tech_contact_email, ssphp_private_ip_address, ssphp_public_ip_address, ssphp_all_ip_addresses, ssphp_created_at, ssphp_updated_at

| outputlookup createinapp=t append=f ssphp_azure_resource_cmdb_complete.csv

| search (source="azure_resource_graph:resource_graph:Resources" OR source="azure_resource_graph:resource_graph:ResourceContainers") AND ssphp_type!="microsoft.insights/metricalerts"
| outputlookup createinapp=t append=f ssphp_azure_resource_cmdb_resources.csv

| search [| inputlookup ssphp_azure_resource_cmdb_included_types.csv
          | search include=1
          | eval st="(ssphp_resource_type_category=\"".category."\" AND ssphp_resource_type_type=\"".type."\")"
          | stats values(st) as st
          | eval st="(".mvjoin(st," OR ").")"
          | return $st]
| outputlookup createinapp=t append=f ssphp_azure_resource_cmdb_included_resources.csv
"""
