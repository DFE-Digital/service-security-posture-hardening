[ssphp_use_case_rollup]
cron_schedule = */5 * * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| search `ssphp_metrics_index` (ssphp.use_case.id="*" AND ssphp.use_case.id!="*rollup*")
    [| search `ssphp_metrics_index` (ssphp.use_case.id="*" AND ssphp.use_case.id!="*rollup*")
     | stats max(SSPHP_RUN) as SSPHP_RUN by ssphp.use_case.id
     | eval search_text="(\"ssphp.use_case.id\"=\"".'ssphp.use_case.id'."\" AND SSPHP_RUN=\"".SSPHP_RUN."\")"
     | stats values(search_text) as search_text
     | eval search_text="(".mvjoin(search_text," OR ").")"
    | return $search_text]

| rex field=ssphp.use_case.id "(?<f1>[^_]*)"
| rex field=ssphp.use_case.id "[^_]*_(?<f2>[^_]*)"
| rex field=ssphp.use_case.id "[^_]*_[^_]*_(?<f3>[^_]*)"
| fields ssphp.use_case.id, ssphp.score.score, f1, f2, f3

| appendpipe 
    [| search f3=*
    | stats min(ssphp.score.score) as ssphp.score.score by f1, f2
    | eval type="L2",
           ssphp.use_case.id=f1."_".f2."_000"
]
| appendpipe 
    [| search NOT f3=*
    | stats min(ssphp.score.score) as ssphp.score.score by f1
    | eval type="L1",
           ssphp.use_case.id=f1."_000"
]
| eval ssphp.score.score=floor('ssphp.score.score')
| search type=*
| fields ssphp.use_case.id, ssphp.score.score,f1, f2

``` add metadata ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.display.title=if(isnull(f2),upper('f1'." ROLLUP"),upper('f1'." ".'f2'." ROLLUP")),
       ssphp.use_case.display.short_description="Application Permissions",
       ssphp.use_case.description="This use case is the rolled up number from all the ".if(isnull(f2),upper('f1'),upper('f1'." ".'f2'))." use cases",
       ssphp.use_case.version="1.0.0",
       ssphp.use_case.last_changed.date="2023-09-18",
       ssphp.use_case.last_changed.by="Ian Pearl",
       ssphp.use_case.treshhold.red="-",
       ssphp.use_case.treshhold.orange="-",
       ssphp.use_case.treshhold.green="99",
       ssphp.color=case('ssphp.score.score'>'ssphp.use_case.treshhold.green',"green",1==1,"red"),
       ssphp.use_case.scoring_narrative="Score is calculated as the average of all the constituent ".if(isnull(f2),upper('f1'),upper('f1'." ".'f2'))." scores. Best=100, Worst=0"
       
| fields - f1, f2, f3

`ssphp_use_case_write`

"""
