[ssphp_use_case_github_001_cis_1-1-3]
cron_schedule = 55 5 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

index="test" sourcetype="github:branch:protection"
    [| search index=test sourcetype="github:branch:protection"
    | stats max(SSPHP_RUN) as SSPHP_RUN
    | return SSPHP_RUN]
    
| fields source, url, repo, branch_protection, required_pull_request_reviews.required_approving_review_count

`ssphp_use_case_add_service_metadata_github`

| eval ssphp.score.non_compliant_fields=if((lower('branch_protection')="false" OR 
                                                 'required_pull_request_reviews.required_approving_review_count'<2 OR 
                                                 isnull('required_pull_request_reviews.required_approving_review_count')),
                                            mvappend('ssphp.score.non_compliant_fields',"branch_protection","required_pull_request_reviews.required_approving_review_count"),
                                            'ssphp.score.non_compliant_fields')

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields')/2,"0"),
       ssphp.score.denominator="1",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields service.name, service.display_name, repo, branch_protection, required_pull_request_reviews.required_approving_review_count, ssphp.score.non_compliant_fields, ssphp.score.score


``` ##################### end dashboard query ##################### ```
| stats count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator, 
        values(service.display_name) as service.display_name 
        by service.name
| eval ssphp.score.score=if('ssphp.score.numerator'>0,"0","100")
       

``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="github_001_cis_1-1-3",
       ssphp.use_case.version.number="1.0.1",
       ssphp.use_case.version.last_changed_date="2024-03-26",
       ssphp.use_case.version.last_changed_by="Sam Pritchard"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.score.scoring_narrative="In order to be compliant, repositories must have branch protection on and the required approver count to be 2 or more."

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


``` metadata that only occurs in our own controls, not in CIS controls where it comes from the benchmark lookup```
| eval ssphp.use_case.title="Github [CIS 1.1.3]",
       ssphp.use_case.foundational_system='service.name',
       ssphp.use_case.foundational_system_name='service.display_name',
       ssphp.use_case.savedsearch="ssphp_use_case_github_001_cis_1-1-3"
    

| search service.name!="-"

`ssphp_use_case_add_cis_metadata_github`

`ssphp_use_case_write`

"""