[ssphp_use_case_azure_001_cis_1-24]
cron_schedule = */30 * * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| `ssphp_use_case_m365_cis_powershell("SSPHP.azure.role_assignment")`

| rename properties.* as *
| fields id, principalId, roleDefinitionId

| rex field=id "^\/subscriptions\/(?<subscription>[^\/]*)\/"
| where isnotnull(subscription) and isnotnull(principalId) and isnotnull(roleDefinitionId)
| stats values(roleDefinitionId) as roleDefinitionId by subscription, principalId
| mvexpand roleDefinitionId


| join type=outer roleDefinitionId
    [| `ssphp_use_case_m365_cis_powershell("SSPHP.azure.role_definitions")`
     | rename id as roleDefinitionId
     | fields roleDefinitionId, properties.permissions{}.actions{}, properties.roleName

     | eval ssphp.score.numerator=if(like('properties.permissions{}.actions{}',"%Microsoft.Authorization/locks/read%"),1,0),
            ssphp.score.numerator=if(like('properties.permissions{}.actions{}',"%Microsoft.Authorization/locks/write%"),'ssphp.score.numerator'+2,'ssphp.score.numerator'),
            ssphp.score.numerator=if(like('properties.permissions{}.actions{}',"%Microsoft.Authorization/locks/delete%"),'ssphp.score.numerator'+4,'ssphp.score.numerator'),
            ssphp.score.numerator=if('properties.roleName'="Resource Lock Administrator",'ssphp.score.numerator'+8,'ssphp.score.numerator')
     | search ssphp.score.numerator>0
     | table roleDefinitionId, properties.permissions{}.actions{}, properties.roleName, ssphp.score.numerator]

| append 
    [| `ssphp_use_case_m365_cis_powershell("SSPHP.azure.subscriptions")`
     | stats values(subscriptionId) as subscription
     | mvexpand subscription
     | eval ssphp.score.numerator=0]
     
| stats max(ssphp.score.numerator) as ssphp.score.numerator by subscription

| sort 0 - ssphp.score.numerator

| stats count as ssphp.score.denominator, sum(eval(if('ssphp.score.numerator'>6,1,0))) as ssphp.score.numerator
| eval ssphp.score.score=floor('ssphp.score.numerator'*100/'ssphp.score.denominator')

| fields ssphp.score.numerator, ssphp.score.denominator, ssphp.score.score


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="azure_001_cis_1-24",

       ssphp.use_case.version.number="1.0.0",
       ssphp.use_case.version.last_changed_date="2023-12-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       
       ssphp.microsoft.description=coalesce('description',"-"),
       ssphp.microsoft.implementation_status=coalesce('implementationStatus',"-")

`ssphp_use_case_add_cis_metadata`

| eval ssphp.use_case.control=coalesce('ssphp.use_case.control','ssphp.cis_benchmark.control.title',"-"),
       ssphp.use_case.description=coalesce('ssphp.use_case.description','ssphp.cis_benchmark.control.description',"-")

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


`ssphp_use_case_write`

"""
