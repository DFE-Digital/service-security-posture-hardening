[ssphp_use_case_aad_rollup]
cron_schedule = 60 * * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
index="ssphp_metrics" (ssphp.use_case.id="aad_*" AND ssphp.use_case.id!="aad_rollup")
    [| search index="ssphp_metrics"
     | stats max(SSPHP_RUN) as SSPHP_RUN by ssphp.use_case.id
     | eval search_text="(\"ssphp.use_case.id\"=\"".'ssphp.use_case.id'."\" AND SSPHP_RUN=\"".SSPHP_RUN."\")"
     | stats values(search_text) as search_text
     | eval search_text="(".mvjoin(search_text," OR ").")"
    | return $search_text]
    
| stats avg(ssphp.score) as ssphp.score
| eval ssphp.score=round('ssphp.score')

``` add metadata ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="aad_rollup",
       ssphp.use_case.description="This use case is the rolled up number from all the other Azure AD use cases",
       ssphp.use_case.version="1.0.0",
       ssphp.use_case.last_changed.date="2023-07-10",
       ssphp.use_case.last_changed.by="Ian Pearl",
       ssphp.use_case.treshhold.red="50",
       ssphp.use_case.treshhold.orange="70",
       ssphp.use_case.treshhold.green="90",
       ssphp.color=case('ssphp.score'<'ssphp.use_case.treshhold.red',"red",'ssphp.score'>'ssphp.use_case.treshhold.green',"green",1==1,"orange")

| makejson SSPHP_RUN, ssphp* output=_raw
| fields _raw

```| spath input=json```
| collect output_format=hec testmode=f addinfo=f index="ssphp_metrics"

"""
