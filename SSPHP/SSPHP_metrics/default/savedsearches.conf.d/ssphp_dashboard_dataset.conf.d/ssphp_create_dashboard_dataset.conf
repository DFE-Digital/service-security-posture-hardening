[ssphp_create_dashboard_dataset]
dispatch.earliest_time = -2d@d
dispatch.latest_time = now
enableSched = 0
search = """

index="ssphp_metrics_summary" earliest=-2d@d latest=now
      (ssphp.use_case.id="azure*" OR ssphp.use_case.id="dns*" OR ssphp.use_case.id="m365*")
      ssphp.use_case.id!="*_000"
      ```ssphp.score.ciso_priority=1```

      [| search index="ssphp_metrics_summary" earliest=-2d@d latest=now (ssphp.use_case.id="azure*" OR ssphp.use_case.id="dns*" OR ssphp.use_case.id="m365*") ssphp.use_case.id!="*_000" ssphp.score.ciso_priority=1
        | stats max(SSPHP_RUN) as SSPHP_RUN by ssphp.use_case.id
        | eval search_text="(\"ssphp.use_case.id\"=\"".'ssphp.use_case.id'."\" AND SSPHP_RUN=\"".SSPHP_RUN."\")"
        | stats values(search_text) as search_text
        | eval search_text="(".mvjoin(search_text," OR ").")"
        | return $search_text]

| append 
    [| search index="ssphp_metrics_summary" earliest=-2d@d latest=now
      (ssphp.use_case.id="azure_001*" OR ssphp.use_case.id="m365_001*")
      ssphp.use_case.id!="*_000"
      ```ssphp.score.ciso_priority=1```

      [| search index="ssphp_metrics_summary" earliest=-2d@d latest=now (ssphp.use_case.id="azure_001*" OR ssphp.use_case.id="m365_001*") ssphp.use_case.id!="*_000" ssphp.score.ciso_priority=1
        | stats max(SSPHP_RUN) as SSPHP_RUN by ssphp.use_case.id
        | eval search_text="(\"ssphp.use_case.id\"=\"".'ssphp.use_case.id'."\" AND SSPHP_RUN=\"".SSPHP_RUN."\")"
        | stats values(search_text) as search_text
        | eval search_text="(".mvjoin(search_text," OR ").")"
        | return $search_text]
        
        | eval ssphp.use_case.foundational_system="AAD"]


 ``` Is this use case compliant ```
| eval ssphp.score.compliance_status=if('ssphp.score.score'=100,"Compliant","Non-Compliant"),
       ssphp.score.compliance_status='ssphp.score.compliance_status'."|".'ssphp.score.color',
       line_type="detail"

| sort 0 ssphp.use_case.foundational_system, ssphp.use_case.id


| appendpipe 
    [| search ssphp.score.ciso_priority=1
      | stats count as total_controls, sum(eval(if(match('ssphp.score.compliance_status',"^Compliant.*"),1,0))) as compliant_controls by ssphp.use_case.foundational_system

     | eval compliant_perc=floor('compliant_controls'*100/'total_controls'),
            compliant_abs='compliant_controls'." of ".'total_controls',
            compliant_color=case(isnull('compliant_perc') OR 'compliant_perc'="","#242526",
                                 'compliant_perc'=100,"#6AB187",
                                 1==1,"#D32D41")

     | fields ssphp.use_case.foundational_system, compliant_perc, compliant_abs, compliant_color
     | eval line_type="summary"]
     
| table line_type, ssphp.use_case.foundational_system, ssphp.use_case.id, ssphp.use_case.title, ssphp.score.score, ssphp.score.color, ssphp.score.compliance_status, ssphp.score.ciso_priority, ssphp.cis_benchmark.control.title, ssphp.cis_benchmark.control.description, ssphp.cis_benchmark.control.level, ssphp.cis_benchmark.controls.ig1, ssphp.cis_benchmark.controls.ig2, ssphp.cis_benchmark.controls.ig3, compliant_perc, compliant_abs, compliant_color


"""
