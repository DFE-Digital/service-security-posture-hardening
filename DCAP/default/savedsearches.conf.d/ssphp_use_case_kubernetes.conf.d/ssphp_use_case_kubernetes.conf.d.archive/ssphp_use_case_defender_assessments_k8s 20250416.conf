[ssphp_use_case_defender_assessments_k8s{{environment}}]
cron_schedule = 30 3 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments*" properties.resourceDetails.ResourceProvider="microsoft.containerservice" earliest=-2d@d latest=now

    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments*" properties.resourceDetails.ResourceProvider="microsoft.containerservice" earliest=-2d@d latest=now
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| spath id
| spath properties.description
| spath properties.displayName
| spath properties.metadata.description
| spath properties.metadata.displayName
| spath properties.resourceDetails.Id
| spath properties.resourceDetails.id
| spath properties.resourceDetails.ResourceName
| spath properties.resourceDetails.ResourceType
| spath ssphp.resource.kubernetes.clusterName
| spath ssphp.resource.name
| spath properties.status.code

| rename id as ssphp.assessment.id, name as ssphp.assessment.name, properties.status.* as ssphp.assessment.status.*
| eval ssphp.assessment.description=case('type'="microsoft.security/assessments/subassessments",'properties.description',
                                         'assessment_id'="c5045ea3-afc6-4006-ab8f-86c8574dbf3d",null(),
                                         1==1,'properties.metadata.description'),
       ssphp.assessment.display_name=if('type'="microsoft.security/assessments/subassessments",'properties.displayName','properties.metadata.displayName'),
       ssphp.assessment.severity=coalesce(if('type'="microsoft.security/assessments/subassessments",'properties.additionalData.vulnerabilityDetails.severity','ssphp.assessment.status.severity'),"Unknown"),
       ssphp.resource.id=coalesce('properties.resourceDetails.Id','properties.resourceDetails.id'),
       ssphp.resource.name=coalesce('properties.resourceDetails.ResourceName',"-"),
       ssphp.resource.type=if('properties.resourceDetails.ResourceType'="microsoft.containerservice/managedclusters","Managed Cluster",'properties.resourceDetails.ResourceType')
       
| eval ssphp.resource.kubernetes.clusterName=coalesce('ssphp.resource.kubernetes.clusterName','ssphp.resource.name')

| fields - assessment_id, sub_assessment_id, ssphp.assessment.status.severity
| where isnotnull('ssphp.assessment.description')

| rename properties.additionalData.kubernetesDetails.* as ssphp.resource.kubernetes.*,
         properties.additionalData.artifactDetails.* as ssphp.resource.artifact.*,
         tenantId as ssphp.resource.tenant, subscriptionId as ssphp.resource.subscription, resourceGroup as ssphp.resource.resource_group

```| search ssphp.assessment.name="00befa02-f8dc-0c7b-101e-23973a37b4d7"```

| eval ssphp.assessment.description=trim(replace(replace('ssphp.assessment.description',"Summary:",""),"\[Generated by AI\]",""))
```| search properties.status.code="Unhealthy"```

| join type=outer ssphp.resource.tenant, ssphp.resource.subscription, ssphp.resource.resource_group
    [| search index="ssphp_asset_inventory{{environment}}" ssphp.resource.source="AZURE" earliest=-2d@d latest=now
     | stats values(ssphp.service.*) as ssphp.service.*
             by ssphp.resource.tenant, ssphp.resource.subscription, ssphp.resource.resource_group

     | table ssphp.resource.tenant, ssphp.resource.subscription, ssphp.resource.resource_group, ssphp.service.*]


| fields ssphp.assessment.*, ssphp.service.*, ssphp.resource.*


``` ##################### replace with dashboard filter ##################### ```


``` ##################### end dashboard query ##################### ```

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id='ssphp.assessment.name',
       ssphp.use_case.title="Microsoft Defender Kubernetes Assessment [".'ssphp.assessment.name'."]",
       ssphp.use_case.savedsearch="ssphp_use_case_defender_assessments_k8s{{environment}}",
       ssphp.use_case.group="AZURE",
       ssphp.use_case.group_name="MS-DEFENDER",
       ssphp.use_case.version.number="1.0.7",
       ssphp.use_case.version.last_changed_date="2025-04-09",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.category="KUBERNETES",
       ssphp.assessment.source="Defender"

| eval ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.ciso_priority=case(lower('ssphp.assessment.severity')="critical","1",
                                      lower('ssphp.assessment.severity')="high","2",
                                      lower('ssphp.assessment.severity')="medium","3",
                                      1==1,"4"),
       ssphp.score.scoring_narrative="In order to be compliant, the value of properties.status.code MUST be 'Healthy' for each of the Resources listed.
Note : the health status relates only to the Assessment checked for this specific Control : 'xxxxxxxxxx'.
All the resources marked as 'Unhealthy' must be remediated"

| fields SSPHP_RUN, ssphp.*


| eval ssphp.score.numerator=coalesce(mvcount(mvfilter(match('ssphp.assessment.status.code',"Unhealthy"))),"0"),
       ssphp.score.denominator=coalesce(mvcount('ssphp.assessment.status.code'),"0"),
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator'),
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

| eval ssphp.risk.expectancy="-",
       ssphp.risk.impact="-",
       ssphp.score.remediation_priority=(5-'ssphp.score.ciso_priority')*(abs('ssphp.score.score'-100) / 100)


| eval SSPHP_UID=md5('ssphp.use_case.category'."|".'ssphp.service.id'."|".'ssphp.use_case.id'."|".'ssphp.resource.id')

| eval ssphp.score.color=case('ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="1","red",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="2","orange",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="3","white",
                              1==1,'ssphp.score.color')

| fields SSPHP_RUN, SSPHP_UID, ssphp.*


`ssphp_use_case_no_write{{environment}}`

"""


