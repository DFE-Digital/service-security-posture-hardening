[ssphp_use_case_m365_004_cis_4-12{{environment}}]
cron_schedule = 40 4 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

| `ssphp_use_case_m365_004_cis_4-12{{environment}}`

| fields sourcetype, Id, Severity, Category, NotificationEnabled, UserTags, Filter, EnablePriorityAccountProtection

| eventstats sum(eval(if(sourcetype="m365:user_vip",1,0))) as number_vips

| makemv ssphp.score.non_compliant_fields
| eval minimum_allowed_number_vips=5,
       ssphp.score.non_compliant_fields=case(sourcetype="m365:user_vip" AND number_vips<'minimum_allowed_number_vips',   ``` Test for number of VIPs```
                                             mvappend('ssphp.score.non_compliant_fields',"number_vips"),
                                             1==1,'ssphp.score.non_compliant_fields'),
                                             
       ssphp.score.non_compliant_fields=case(sourcetype="m365:email_tenant_settings" AND NOT lower('EnablePriorityAccountProtection')="true",    ``` Test for number of Priority Account protection being set on for the tenancy```
                                             mvappend('ssphp.score.non_compliant_fields',"EnablePriorityAccountProtection"),
                                             1==1,'ssphp.score.non_compliant_fields'),
                                             
       ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT lower('Severity')="high",   ``` Multiple Tests for existance of 2 specific Alert Policies with settings as they should be```
                                             mvappend('ssphp.score.non_compliant_fields',"Severity"),
                                             1==1,'ssphp.score.non_compliant_fields'),
       ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT lower('Category')="threatmanagement",
                                             mvappend('ssphp.score.non_compliant_fields',"Category"),
                                             1==1,'ssphp.score.non_compliant_fields'),
       ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT lower('UserTags')="priority account",
                                             mvappend('ssphp.score.non_compliant_fields',"UserTags"),
                                             1==1,'ssphp.score.non_compliant_fields'),
       ```ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT like(Filter,"Activity.User.Tags -like 'Priority account'"), *** NOT OVERTLY SPECIFIED IN CIS BENCHMARK ***
                                             mvappend('ssphp.score.non_compliant_fields',"Filter"),
                                             1==1,'ssphp.score.non_compliant_fields'),```
       ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT (like(Id,"%Detected malware in an e-mail message%") OR like(Id,"%Phishing email detected at time of delivery%")),
                                             mvappend('ssphp.score.non_compliant_fields',"Id"),
                                             1==1,'ssphp.score.non_compliant_fields'),
       ssphp.score.non_compliant_fields=case(sourcetype="m365:protection_alert" AND NOT lower('NotificationEnabled')="true",
                                             mvappend('ssphp.score.non_compliant_fields',"NotificationEnabled"),
                                             1==1,'ssphp.score.non_compliant_fields')

| eventstats sum(eval(if(sourcetype="m365:protection_alert",1,0))) as number_protection_alerts_total,
             sum(eval(if(sourcetype="m365:protection_alert" AND (mvcount(ssphp.score.non_compliant_fields)=0OR isnull('ssphp.score.non_compliant_fields')),1,0))) as number_protection_alerts_compliant

| eval number_protection_alerts_total=if(sourcetype="m365:protection_alert",'number_protection_alerts_total',null()),
       number_protection_alerts_compliant=if(sourcetype="m365:protection_alert",'number_protection_alerts_compliant',null()),
       number_vips=if(sourcetype="m365:user_vip",'number_vips',null()),
       minimum_allowed_number_vips=if(sourcetype="m365:user_vip",'minimum_allowed_number_vips',null())

| fields sourcetype, Id, Severity, Category, NotificationEnabled, UserTags, Filter, EnablePriorityAccountProtection, minimum_allowed_number_vips, number_vips, number_protection_alerts_total, number_protection_alerts_compliant, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```


| stats values(number_vips) as number_vips,
        values(minimum_allowed_number_vips) as minimum_allowed_number_vips,
        sum(eval(if(sourcetype="m365:email_tenant_settings",1,0))) as mail_tenant_settings_total,
        sum(eval(if(sourcetype="m365:email_tenant_settings" AND (mvcount('ssphp.score.non_compliant_fields')=0 OR isnull('ssphp.score.non_compliant_fields')),1,0))) as mail_tenant_settings_compliant,
        values(number_protection_alerts_total) as number_protection_alerts_total,
        values(number_protection_alerts_compliant) as number_protection_alerts_compliant

| eval test_1=if('number_vips'>='minimum_allowed_number_vips',1,0),
       test_2=if('mail_tenant_settings_total'=1 AND 'mail_tenant_settings_compliant'=1,1,0),
       test_3=if('number_protection_alerts_compliant'>=2,1,0),
       ssphp.score.numerator='test_1' + 'test_2' + 'test_3',
       ssphp.score.denominator=3,
       ssphp.score.score=floor('ssphp.score.numerator'*100/'ssphp.score.denominator')
       
| table ssphp.score.numerator, ssphp.score.denominator, ssphp.score.score

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="m365_004_cis_4-12",

       ssphp.use_case.version.number="3.0.0",
       ssphp.use_case.version.last_changed_date="2025-02-05",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       
       ssphp.microsoft.description=coalesce('description',"-"),
       ssphp.microsoft.implementation_status=coalesce('implementationStatus',"-")

`ssphp_use_case_add_cis_metadata_m365{{environment}}`

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.source.service="M365",
       ssphp.source.service_name="M365",
       ssphp.use_case.savedsearch="ssphp_use_case_m365_004_cis_4-12{{environment}}"

| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :~~
IF sourcetype = 'm365:protection_alert' :
   Severity = 'high'
   Category = 'ThreatManagement'
   NotificationEnabled = 'true'
   UserTags = 'priority account'
   there must be 1 record for Alert Policy with Id of 'Detected malware in an e-mail message' and another with Od of 'Phishing email detected at time of delivery'~~
IF sourcetype = 'm365:email_tenant_settings' :
   EnablePriorityAccountProtection = 'true'~~
IF sourcetype = 'm365:user_vip' :
   at least the number of VIP accounts as the value set in minimum_allowed_number_vips"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

`ssphp_use_case_write{{environment}}`

"""
