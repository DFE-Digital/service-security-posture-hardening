[ssphp_use_case_m365_001_cis_1-7{{environment}}]
cron_schedule = 10 4 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| `ssphp_use_case_msgraph{{environment}}("/v1.0/identity/conditionalAccess/policies")`

| fields displayName, conditions.applications.includeApplications{}, conditions.clientAppTypes{}, conditions.users.includeUsers{}, state, sessionControls.applicationEnforcedRestrictions.isEnabled

| append
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ssphp:ms_graph:json"
         [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ssphp:ms_graph:json"
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
 
     | spath type 
     | search type=ActivityBasedTimeoutPolicy

     | spath definition{}
     | spath input=definition{} output=WebSessionIdleTimeout path=ActivityBasedTimeoutPolicy{}.ApplicationPolicies{}.WebSessionIdleTimeout

     | eval hours=mvindex(split('WebSessionIdleTimeout',":"),0), minutes=mvindex(split('WebSessionIdleTimeout',":"),1), seconds=mvindex(split('WebSessionIdleTimeout',":"),2),
            WebSessionIdleTimeout_secs=('hours'*60*60)+('minutes'*60)+'seconds',
            ssphp_source="append"
     | table WebSessionIdleTimeout_secs, ssphp_source]

| eventstats values(WebSessionIdleTimeout_secs) as WebSessionIdleTimeout_secs
| search NOT ssphp_source="append"

| fields WebSessionIdleTimeout_secs, displayName, conditions.applications.includeApplications{}, conditions.clientAppTypes{}, conditions.users.includeUsers{}, state, sessionControls.applicationEnforcedRestrictions.isEnabled
       
       
| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(state="enabled",'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"state")),
       ssphp.score.non_compliant_fields=if('conditions.applications.includeApplications{}'="Office365",'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"conditions.applications.includeApplications{}")),
       ssphp.score.non_compliant_fields=if('conditions.clientAppTypes{}'="browser",'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"conditions.clientAppTypes{}")),
       ssphp.score.non_compliant_fields=if('conditions.users.includeUsers{}'="All",'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"conditions.users.includeUsers{}")),
       ssphp.score.non_compliant_fields=if('sessionControls.applicationEnforcedRestrictions.isEnabled'="true",'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"sessionControls.applicationEnforcedRestrictions.isEnabled")),
       ssphp.score.non_compliant_fields=if('WebSessionIdleTimeout_secs'<=3600,'ssphp.score.non_compliant_fields',mvappend('ssphp.score.non_compliant_fields',"WebSessionIdleTimeout_secs"))

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="6",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')


| table displayName, WebSessionIdleTimeout_secs, conditions.applications.includeApplications{}, conditions.clientAppTypes{}, conditions.users.includeUsers{}, state, sessionControls.applicationEnforcedRestrictions.isEnabled
         ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```


| stats max(ssphp.score.score) as ssphp.score.score, min(ssphp.score.numerator) as ssphp.score.numerator, max(ssphp.score.denominator) as ssphp.score.denominator

`ssphp_use_case_write_null_output_override_DEV(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="m365_001_cis_1-7",

       ssphp.use_case.version.number="3.0.2",
       ssphp.use_case.version.last_changed_date="2024-11-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       
       ssphp.microsoft.description=coalesce('description',"-"),
       ssphp.microsoft.implementation_status=coalesce('implementationStatus',"-")

`ssphp_use_case_add_cis_metadata_m365{{environment}}`

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="2",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.source.service="M365",
       ssphp.source.service_name="M365",
       ssphp.use_case.savedsearch="ssphp_use_case_m365_001_cis_1-7{{environment}}"


| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :~~
state='enabled'
conditions.applications.includeApplications{}='Office365'
conditions.clientAppTypes{}='browser'
conditions.users.includeUsers{}='All'
sessionControls.applicationEnforcedRestrictions.isEnabled='true'
WebSessionIdleTimeout_secs<=3600"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


`ssphp_use_case_write{{environment}}`

"""
