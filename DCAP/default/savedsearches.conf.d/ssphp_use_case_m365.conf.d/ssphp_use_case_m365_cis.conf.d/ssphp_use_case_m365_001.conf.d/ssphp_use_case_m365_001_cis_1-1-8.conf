[ssphp_use_case_m365_001_cis_1-1-8{{environment}}]
cron_schedule = 10 4 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
`ssphp_metrics_data_index{{environment}}` source="/beta/reports/authenticationMethods/userRegistrationDetails" earliest=-7d@d latest=now
    [| search `ssphp_metrics_data_index{{environment}}` source="/beta/reports/authenticationMethods/userRegistrationDetails" earliest=-7d@d latest=now
    | stats max(SSPHP_RUN) as SSPHP_RUN
    | return SSPHP_RUN]

| stats values(isSsprCapable) as isSsprCapable, values(isSsprEnabled) as isSsprEnabled, values(isSsprRegistered) as isSsprRegistered by userPrincipalName


| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(isSsprEnabled="true",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"isSsprEnabled"))

| sort 0 isSsprEnabled, userPrincipalName
| fields userPrincipalName, isSsprEnabled, isSsprCapable, isSsprRegistered, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```

| stats count  as total_users, sum(eval(if(isSsprEnabled="true",1,0))) as total_user_with_spr_on, sum(eval(if(isSsprEnabled!="true",1,0))) as total_user_with_spr_off

| eval ssphp.score.numerator='total_user_with_spr_off', 
       ssphp.score.denominator='total_users',
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')
| fillnull value=0 ssphp.score.score
| fillnull value="-" ssphp.score.numerator, ssphp.score.denominator


| fields ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="m365_001_cis_1-1-8",

       ssphp.use_case.version.number="4.0.0",
       ssphp.use_case.version.last_changed_date="2025-02-25",
       ssphp.use_case.version.last_changed_by="Ian Pearl"

`ssphp_use_case_add_cis_metadata_m365{{environment}}`

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.source.service="M365",
       ssphp.source.service_name="M365",
       ssphp.use_case.savedsearch="ssphp_use_case_m365_001_cis_1-1-8{{environment}}"


| eval ssphp.score.scoring_narrative="Each user must have the value of 'isSsprEnabled' set to 'true'."

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


`ssphp_use_case_write{{environment}}`

"""
