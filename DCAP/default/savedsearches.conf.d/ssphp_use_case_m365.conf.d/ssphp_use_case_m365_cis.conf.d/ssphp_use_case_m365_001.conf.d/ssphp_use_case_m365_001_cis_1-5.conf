[ssphp_use_case_m365_001_cis_1-5{{environment}}]
cron_schedule = 10 4 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| `ssphp_use_case_m365_001_cis_1-5{{environment}}`

| fields conditionalAccessPolicies{}.displayName, onPremisesSyncEnabled, assignedPlans{}.servicePlanId

| eval i1=if(like('assignedPlans{}.servicePlanId',"%41781fb2-bc02-4b7c-bd55-b576c07bb09d%"),1,0),
       i2=if(like('assignedPlans{}.servicePlanId',"%eec0eb4f-6444-4f95-aba0-50c24d67f998%"),1,0),
       i3=i1+i2

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(isnull('onPremisesSyncEnabled'),
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"onPremisesSyncEnabled")),
       ssphp.score.non_compliant_fields=if('i3'=mvcount('assignedPlans{}.servicePlanId'),
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"assignedPlans{}.servicePlanId"))

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="2",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')
       
| fields conditionalAccessPolicies{}.displayName, onPremisesSyncEnabled, assignedPlans{}.servicePlanId, ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields

``` ##################### end dashboard query ##################### ```


| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="m365_001_cis_1-5",

       ssphp.use_case.version.number="2.0.2",
       ssphp.use_case.version.last_changed_date="2024-07-04",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       
       ssphp.microsoft.description=coalesce('description',"-"),
       ssphp.microsoft.implementation_status=coalesce('implementationStatus',"-")

`ssphp_use_case_add_cis_metadata_m365{{environment}}`

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="2",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.source.service="M365",
       ssphp.source.service_name="M365",
       ssphp.use_case.savedsearch="ssphp_use_case_m365_001_cis_1-5{{environment}}"


| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :~~
onPremisesSyncEnabled is empty  ie null~~
assignedPlans{}.servicePlanId MUST have a value AND that value MUST be either or both of~~
'41781fb2-bc02-4b7c-bd55-b576c07bb09d' AND/OR 'eec0eb4f-6444-4f95-aba0-50c24d67f998'~~
(but no other values are compliant)"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


`ssphp_use_case_write{{environment}}`

"""
