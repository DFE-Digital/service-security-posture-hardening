[ssphp_use_case_aad_dfe_2-0{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
`ssphp_use_case_aad_admin_users{{environment}}`

| fields user_id, userPrincipalName, isAdmin, isPrivileged, cap_id
| mvexpand cap_id

| join type=outer cap_id
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ssphp:ms_graph:json" source="/v1.0/identity/conditionalAccess/policies" earliest=-7d
         [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ssphp:ms_graph:json" source="/v1.0/identity/conditionalAccess/policies" earliest=-7d
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
     
     | spath id
     | spath displayName
     | spath grantControls.builtInControls{}
     | spath grantControls.authenticationStrength.requirementsSatisfied
     | spath state, 
     | spath conditions.applications.includeApplications{}
     | spath conditions.clientAppTypes{}

     | fields id, displayName, grantControls.builtInControls{}, grantControls.authenticationStrength.requirementsSatisfied, state, conditions.applications.includeApplications{}, conditions.clientAppTypes{} 
     | rename * as cap_*, *{} as *
     | table cap_*]

| eval cap_conditions.clientAppTypes=split('cap_conditions.clientAppTypes',"
")
| eval cap_grantControls.builtInControls=split('cap_grantControls.builtInControls',"
")

| fillnull value="-" cap_conditions.applications.includeApplications, cap_conditions.clientAppTypes, cap_state, cap_grantControls.builtInControls, cap_grantControls.authenticationStrength.requirementsSatisfied
| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(upper('cap_conditions.applications.includeApplications')="ALL",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields', "cap_conditions.applications.includeApplications")),
       ssphp.score.non_compliant_fields=if(upper('cap_conditions.clientAppTypes')="ALL",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields', "cap_conditions.clientAppTypes")),
       ssphp.score.non_compliant_fields=if(upper('cap_state')="ENABLED",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields', "cap_state")),
       ssphp.score.non_compliant_fields=if(upper('cap_grantControls.builtInControls')="MFA" OR upper('cap_grantControls.authenticationStrength.requirementsSatisfied')="MFA",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields', "cap_grantControls.builtInControls", "cap_grantControls.authenticationStrength.requirementsSatisfied"))

| eval cap_conditions.clientAppTypes=mvjoin('cap_conditions.clientAppTypes',"
")
| eval cap_grantControls.builtInControls=mvjoin('cap_grantControls.builtInControls',"
")

| eval compliant_policy=if(isnull('ssphp.score.non_compliant_fields'),"1","0")

| fields user_id, userPrincipalName, isAdmin, isPrivileged, 
         cap_id, cap_displayName, cap_conditions.applications.includeApplications, cap_conditions.clientAppTypes, cap_state, cap_grantControls.builtInControls, cap_grantControls.authenticationStrength.requirementsSatisfied,
         compliant_policy, ssphp.score.non_compliant_fields

| stats values(userPrincipalName) as userPrincipalName,
        values(isAdmin) as isAdmin, values(isPrivileged) as isPrivileged,
        sum(eval(if('compliant_policy'>0,1,0))) as number_compliant_policies,
        list(cap_*) as cap_*,
        list(cap_displayName) as cap_displayName,
        values(eval(if('compliant_policy'="1",'cap_id'."::".'cap_displayName',null()))) as compliant_policies,
        values(eval(if('compliant_policy'="0",'cap_id'."::".'cap_displayName',null()))) as non_compliant_policies,
        values(ssphp.score.non_compliant_fields) as ssphp.score.non_compliant_fields
        by user_id


| rename number_compliant_policies as ssphp.score.numerator
| eval ssphp.score.denominator=mvcount('cap_displayName'),
       ssphp.score.score=if('ssphp.score.numerator'>0,100,0),
       ssphp.score.non_compliant_fields=if('ssphp.score.score'=100,null(),'ssphp.score.non_compliant_fields')
       
| table user_id, userPrincipalName, isAdmin, isPrivileged, compliant_policies, non_compliant_policies, 
        cap_displayName, cap_conditions.applications.includeApplications, cap_conditions.clientAppTypes, cap_state, cap_grantControls.builtInControls, cap_grantControls.authenticationStrength.requirementsSatisfied
        ssphp.score.numerator, ssphp.score.denominator, ssphp.score.score, ssphp.score.non_compliant_fields

``` ##################### end dashboard query ##################### ```

| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="aad_dfe_2-0",

       ssphp.use_case.version.number="3.1.9",
       ssphp.use_case.version.last_changed_date="2025-11-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl& Alex Kinnane"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.score.scoring_narrative="In order to be compliant, each priviledged / admin User must have at least Conditonal Access Policy applied to their account which meets the following conditions :
Enabled = true
cap_conditions.applications.includeApplications = ALL
cap_conditions.clientAppTypes = ALL
cap_grantControls.builtInControls = MFA OR cap_grantControls.authenticationStrength.requirementsSatisfied = MFA"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


``` metadata that only occurs in our own controls, not in CIS controls where it comes from the benchmark lookup```
| eval ssphp.use_case.title="AAD [DfE 2.0]",
       ssphp.source.foundational_system="AAD",
       ssphp.source.service="AAD",
       ssphp.source.service_name="AAD",
       ssphp.benchmark.origin="DfE",
       ssphp.benchmark.control.title="Admins no MFA Conditional Policies",
       ssphp.benchmark.control.description="Privileged AD accounts that do not have a specific conditional access policy",
       ssphp.benchmark.control.rationale="All privileged Azure AD users should have an MFA required policy enabled on their account.",
       ssphp.benchmark.control.impact="-",
       ssphp.use_case.savedsearch="ssphp_use_case_aad_dfe_2-0{{environment}}"


`ssphp_use_case_write{{environment}}`

"""
