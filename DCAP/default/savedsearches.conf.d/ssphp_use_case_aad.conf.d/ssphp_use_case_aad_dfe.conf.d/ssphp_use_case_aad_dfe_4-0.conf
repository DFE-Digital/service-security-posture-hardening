[ssphp_use_case_aad_dfe_4-0{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_use_case_aad_admin_users{{environment}}`

| fields user_id, userPrincipalName, isAdmin, isPrivileged, cap_id

| mvexpand cap_id

| join type=outer cap_id 
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="SSPHP.AAD.conditional_access_policy" earliest=-7d 
        [| search `ssphp_metrics_data_index{{environment}}` sourcetype="SSPHP.AAD.conditional_access_policy" earliest=-7d 
         | stats max(SSPHP_RUN) as SSPHP_RUN 
         | return SSPHP_RUN] 

    | spath "conditions.locations.includeLocations{}" 
    | spath "conditions.locations.excludeLocations{}" 
    | spath id 
    | spath displayName 
    | spath hasUntrustedConditions{}
    | spath "grantControls.builtInControls{}" 
    | spath grantControls.authenticationStrength.requirementsSatisfied 
    | spath state, 
    | spath conditions.applications.includeApplications{} 
    | spath conditions.clientAppTypes{} 

    | fields - _raw, eventtype, host, index, linecount, punct, source, sourcetype, splunk_server, splunk_server_group, tag, tag::eventtype, tag::sourcetype
    | fields id, displayName, conditions.locations.*, grantControls.builtInControls{}, state, conditions.applications.includeApplications{}, conditions.clientAppTypes{}, conditions.locations.excludeLocations{}, conditions.locations.includeLocations{}, hasUntrustedConditions{}

    | eval excluded_locations='conditions.locations.excludeLocations{}', included_locations='conditions.locations.includeLocations{}' 
    | eval location_ids=mvzip('excluded_locations', 'included_locations', "===") 
    | fields - excluded_locations, included_locations 

    | mvexpand location_ids 
    | eval excluded_locations=mvindex(split('location_ids', "==="), 0), included_locations=mvindex(split('location_ids', "==="), 1) 
    | fields - location_ids 

    | join type=outer excluded_locations max=0 
        [| search `ssphp_metrics_data_index{{environment}}` source="/v1.0/identity/conditionalAccess/namedLocations" earliest=-7d 
           [| search `ssphp_metrics_data_index{{environment}}` source="/v1.0/identity/conditionalAccess/namedLocations" earliest=-7d 
            | stats max(SSPHP_RUN) as SSPHP_RUN
            | return SSPHP_RUN]
         | table id displayName isTrusted ipRanges{}.* 
         | fillnull value=false isTrusted 
         | rename * as excluded_locations_* 
         | eval excluded_locations='excluded_locations_id'] 

    | join type=outer included_locations max=0 
        [| search `ssphp_metrics_data_index{{environment}}` source="/v1.0/identity/conditionalAccess/namedLocations" earliest=-7d
           [| search `ssphp_metrics_data_index{{environment}}` source="/v1.0/identity/conditionalAccess/namedLocations" earliest=-7d
            | stats max(SSPHP_RUN) as SSPHP_RUN
            | return SSPHP_RUN]
         | table id displayName isTrusted ipRanges{}.* 
         | fillnull value=false isTrusted 
         | rename * as included_locations_* 
         | eval included_locations='included_locations_id'] 

    | fields id, displayName, state, grantControls.builtInControls{}, excluded_locations, excluded_locations_displayName, excluded_locations_isTrusted, excluded_locations_ipRanges{}.@odata.type, included_locations, included_locations_displayName, included_locations_isTrusted, conditions.applications.includeApplications{}, conditions.clientAppTypes{}, hasUntrustedConditions{}
    
    | stats values(*) as * by id 
    | rename * as cap_*]

| rename *{} as *
| eval cap_conditions.clientAppTypes=split('cap_conditions.clientAppTypes'," "),
       cap_grantControls.builtInControls=split('cap_grantControls.builtInControls'," ")
| eval cap_hasUntrustedConditions=split('cap_hasUntrustedConditions',"
")

```| search cap_excluded_locations_isTrusted="true" AND 
         cap_included_locations="All" AND 
         cap_state="enabled" AND 
         'cap_grantControls.builtInControls{}'="block" AND
         'cap_conditions.applications.includeApplications{}'="All" AND 
         (cap_clientAppTypes="All" OR (cap_clientAppTypes="exchangeActiveSync" AND cap_clientAppTypes="browser" AND cap_clientAppTypes="mobileAppsAndDesktopClients" AND cap_clientAppTypes="other"))```

| fillnull value="-" cap_hasUntrustedConditions
| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(upper('cap_state')="ENABLED",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"cap_state")),
       ssphp.score.non_compliant_fields=if(upper('cap_grantControls.builtInControls')="COMPLIANTDEVICE",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"cap_grantControls.builtInControls")),
       ssphp.score.non_compliant_fields=if(upper('cap_conditions.applications.includeApplications')="ALL",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"cap_conditions.applications.includeApplications")),
       ssphp.score.non_compliant_fields=if((upper('cap_conditions.clientAppTypes')="ALL" OR (upper('cap_conditions.clientAppTypes')="EXCHANGEACTIVESYNC" AND upper('cap_conditions.clientAppTypes')="BROWSER" AND upper('cap_conditions.clientAppTypes')="MOBILEAPPSANDDESKTOPCLIENTS" AND upper('cap_conditions.clientAppTypes')="OTHER")),
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"cap_conditions.clientAppTypes")),
       ssphp.score.non_compliant_fields=if('cap_hasUntrustedConditions'="-",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields', "cap_hasUntrustedConditions"))


| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="5",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields user_id, userPrincipalName, isAdmin, isPrivileged,
        cap_displayName,
        cap_id,
        cap_state,
        cap_grantControls.builtInControls,
        cap_conditions.applications.includeApplications,
        cap_conditions.clientAppTypes,
        cap_hasUntrustedConditions,
        ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```


| stats max(ssphp.score.denominator) as ssphp.score.denominator,
        min(ssphp.score.numerator) as ssphp.score.numerator,
        max(ssphp.score.score) as ssphp.score.score,
        values(eval(if('ssphp.score.score'="100",null(),'ssphp.score.non_compliant_fields'))) as ssphp.score.non_compliant_fields
        by user_id


| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="aad_dfe_4-0",

       ssphp.use_case.version.number="3.2.0",
       ssphp.use_case.version.last_changed_date="2025-11-14",
       ssphp.use_case.version.last_changed_by="Ian Pearl & Alex Kinnane"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :
cap_state=ENABLED
cap_grantControls.builtInControls=COMPLIANTDEVICE
cap_conditions.applications.includeApplications=ALL
cap_conditions.clientAppTypes=ALL OR 
cap_conditions.clientAppTypes=EXCHANGEACTIVESYNC AND cap_conditions.clientAppTypes=BROWSER AND cap_conditions.clientAppTypes=MOBILEAPPSANDDESKTOPCLIENTS AND cap_conditions.clientAppTypes=OTHER"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


``` metadata that only occurs in our own controls, not in CIS controls where it comes from the benchmark lookup```
| eval ssphp.use_case.title="AAD [DfE 4.0]",
       ssphp.source.foundational_system="AAD",
       ssphp.source.service="AAD",
       ssphp.source.service_name="AAD",
       ssphp.benchmark.origin="DfE",
       ssphp.benchmark.control.title="Admins no Compliant Device Conditional Policies",
       ssphp.benchmark.control.description="Privileged AD accounts that do not have a specific conditional access policy which restricts the devices they can log into AAD from to specific 'compliant devices'.",
       ssphp.benchmark.control.rationale="All users should have a Conditional Access Policy associated with their Azure AD account which restricts access to only be using a restricted device.",
       ssphp.benchmark.control.impact="-",
       ssphp.use_case.savedsearch="ssphp_use_case_aad_dfe_4-0{{environment}}"


`ssphp_use_case_write{{environment}}`

"""
