[ssphp_use_case_aad_dfe_2-0{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
`ssphp_metrics_data_index_DEV` sourcetype="SSPHP.AAD.user" earliest=-7d
        [| search `ssphp_metrics_data_index_DEV` sourcetype="SSPHP.AAD.user" isPrivileged="true" earliest=-7d
    	 | stats max(SSPHP_RUN) as SSPHP_RUN
	     | return SSPHP_RUN]
| rename id as user_id

| join type=outer user_id 
    [| search `ssphp_metrics_data_index_DEV` source="/beta/reports/authenticationMethods/userRegistrationDetails" earliest=-7d
      [| search `ssphp_metrics_data_index_DEV` source="/beta/reports/authenticationMethods/userRegistrationDetails" earliest=-7d
       | stats max(SSPHP_RUN) as SSPHP_RUN
       | return SSPHP_RUN]
   | rename id as user_id
   | table user_id, isAdmin]

| fillnull value="false" isPrivileged, isAdmin
| search isAdmin="true" OR isPrivileged="true"


| rename conditionalAccessPolicies{}.id as cap_id
| table user_id, userPrincipalName, isAdmin, isPrivileged, cap_id
| mvexpand cap_id

| join type=outer cap_id
    [| search index=ssphp_metrics_data sourcetype="ssphp:ms_graph:json" source="/v1.0/identity/conditionalAccess/policies" earliest=-7d
         [| search index=ssphp_metrics_data sourcetype="ssphp:ms_graph:json" source="/v1.0/identity/conditionalAccess/policies" earliest=-7d
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
     
     | spath id
     | spath displayName
     | spath grantControls.builtInControls{}
     | spath grantControls.authenticationStrength.requirementsSatisfied
     | spath state, conditions.applications.includeApplications{}
     | spath conditions.clientAppTypes{}

     | fields id, displayName, grantControls.builtInControls{}, grantControls.authenticationStrength.requirementsSatisfied, state, conditions.applications.includeApplications{}, conditions.clientAppTypes{} 
     | rename * as cap_*, *{} as *
     | table cap_*]
     
| eval compliant_policy=if(upper('cap_conditions.applications.includeApplications')="ALL" AND
                           upper('cap_conditions.clientAppTypes')="ALL" AND
                           upper('cap_state')="ENABLED" AND 
                          (upper('cap_grantControls.builtInControls')="MFA" OR
                           upper('cap_grantControls.authenticationStrength.requirementsSatisfied')="MFA"),
                           "1","0")

| fillnull value="-" cap_conditions.applications.includeApplications, cap_conditions.clientAppTypes, cap_state, cap_grantControls.builtInControls, cap_grantControls.authenticationStrength.requirementsSatisfied
                     
| stats values(userPrincipalName) as userPrincipalName,
        values(isAdmin) as isAdmin, values(isPrivileged) as isPrivileged,
        sum(eval(if('compliant_policy'>0,1,0))) as number_compliant_policies,
        list(cap_*) as cap_*,
        list(cap_displayName) as cap_displayName,
        values(eval(if('compliant_policy'>0,'cap_id'."::".'cap_displayName',null()))) as compliant_policies,
        values(eval(if('compliant_policy'<1,'cap_id'."::".'cap_displayName',null()))) as non_compliant_policies
        by user_id


| rename number_compliant_policies as ssphp.score.numerator
| eval ssphp.score.denominator=mvcount('cap_displayName'),
       ssphp.score.score=if('ssphp.score.numerator'>0,100,0)
       
| table user_id, userPrincipalName, isAdmin, isPrivileged, compliant_policies, non_compliant_policies, 
        cap_displayName, cap_conditions.applications.includeApplications, cap_conditions.clientAppTypes, cap_state, cap_grantControls.builtInControls, cap_grantControls.authenticationStrength.requirementsSatisfied
        ssphp.score.numerator, ssphp.score.denominator, ssphp.score.score

``` ##################### end dashboard query ##################### ```


| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override_DEV(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="aad_dfe_2-0",

       ssphp.use_case.version.number="3.1.1",
       ssphp.use_case.version.last_changed_date="2025-11-05",
       ssphp.use_case.version.last_changed_by="Ian Pearl& Alex Kinnane"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.score.scoring_narrative="In order to be compliant, each priviledged / admin User must have at least Conditonal Access Policy applied to their account which meets the following conditions :
Enabled = true
cap_conditions.applications.includeApplications = ALL
cap_conditions.clientAppTypes = ALL
cap_grantControls.builtInControls = MFA OR cap_grantControls.authenticationStrength.requirementsSatisfied = MFA"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


``` metadata that only occurs in our own controls, not in CIS controls where it comes from the benchmark lookup```
| eval ssphp.use_case.title="AAD [DfE 2.0]",
       ssphp.source.foundational_system="AAD",
       ssphp.source.service="AAD",
       ssphp.source.service_name="AAD",
       ssphp.benchmark.origin="DfE",
       ssphp.benchmark.control.title="Admins no MFA Conditional Policies",
       ssphp.benchmark.control.description="Privileged AD accounts that do not have a specific conditional access policy",
       ssphp.benchmark.control.rationale="All privileged Azure AD users should have an MFA required policy enabled on their account.",
       ssphp.benchmark.control.impact="-",
       ssphp.use_case.savedsearch="ssphp_use_case_aad_dfe_2-0{{environment}}"


`ssphp_use_case_write{{environment}}`

"""
