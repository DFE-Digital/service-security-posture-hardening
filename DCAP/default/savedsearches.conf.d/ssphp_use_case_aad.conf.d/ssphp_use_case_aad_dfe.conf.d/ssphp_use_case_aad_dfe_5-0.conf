[ssphp_use_case_aad_dfe_5-0{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| `ssphp_use_case_aad_002{{environment}}`

```| stats count as total_number_accounts,
        sum(eval(if(isPrivileged="true",1,0))) as total_number_privileged_accounts,
        sum(eval(if(isPrivileged="true" AND match(surname,".*\d+.*"),1,0))) as number_anonymous_accounts


| eval ssphp.score.numerator=number_anonymous_accounts,
       ssphp.score.denominator=total_number_privileged_accounts,
       ssphp.score.score=round('ssphp.score.numerator'*100/'ssphp.score.denominator')
| fillnull value=0 ssphp.score.score
| fields ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator

`ssphp_use_case_write_null_output_override{{environment}}(0)` ```

| search isPrivileged="true"
| fields displayName, surname, givenName, memberships, id, isPrivileged

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(isnull('surname') OR match(surname,".*\d+.*"),   ```proper names are alpha, no numerics ```
                                           mvappend('ssphp.score.non_compliant_fields',"surname"),
                                           'ssphp.score.non_compliant_fields')

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="1",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields displayName, surname, givenName, memberships, id, isPrivileged, ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields

``` ##################### end dashboard query ##################### ```


| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override{{environment}}(0)`



``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="aad_dfe_5-0",

       ssphp.use_case.version.number="3.0.2",
       ssphp.use_case.version.last_changed_date="2024-04-02",
       ssphp.use_case.version.last_changed_by="Ian Pearl"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :~~
surname MUST NOT be null and MUST NOT contain numerics - real surnames do not contain numbers so presumably these are anonymous accounts"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


``` metadata that only occurs in our own controls, not in CIS controls where it comes from the benchmark lookup```
| eval ssphp.use_case.title="AAD [DfE 5.0]",
       ssphp.source.foundational_system="AAD",
       ssphp.source.service="AAD",
       ssphp.source.service_name="AAD",
       ssphp.benchmark.origin="DfE",
       ssphp.benchmark.control.title="Admins with Anonymous Accounts",
       ssphp.benchmark.control.description="Privileged AD accounts that do not have a specific named individual associated",
       ssphp.benchmark.control.rationale="All privileged Azure AD user accounts should be assigned to an individual, not anonymously.",
       ssphp.benchmark.control.impact="-",
       ssphp.use_case.savedsearch="ssphp_use_case_aad_dfe_5-0{{environment}}"


`ssphp_use_case_write{{environment}}`

"""
