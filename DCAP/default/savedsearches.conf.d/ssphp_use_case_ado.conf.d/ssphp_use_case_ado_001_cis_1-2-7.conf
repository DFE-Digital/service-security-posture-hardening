[ssphp_use_case_ado_001_cis_1-2-7{{environment}}]
cron_schedule = 10 6 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_use_case_ado_data{{environment}}`


| fields ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.project_name, ssphp.resource.repo_id, ssphp.resource.repo_name, tenant, repo_url,
         policy_id, policy_type_id, policy_type_display_name, policy_revision,
         repo_isdisabled, repo_ismaintenance, repo_daysSinceLastCommit, repo_mostRecentCommitDate
         ssphp.service.*


| eval target_max_repo_daysSinceLastCommit=180,
       repo_compliant=if('repo_daysSinceLastCommit'<='target_max_repo_daysSinceLastCommit',"true","false")


| stats values(ssphp.resource.*) as ssphp.resource.*, 
        values(repo_compliant) as repo_compliant,
        values(repo_daysSinceLastCommit) as repo_daysSinceLastCommit,
        values(repo_mostRecentCommitDate) as repo_mostRecentCommitDate,
        values(ssphp.service.*) as ssphp.service.*

        by tenant, ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.repo_id

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if('repo_compliant'="true",
                                            'ssphp.score.non_compliant_fields',
                                            mvappend('ssphp.score.non_compliant_fields',"repo_daysSinceLastCommit"))

| eval ssphp.score.remediation_priority=if('repo_compliant'="true","0","20")

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="1",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| sort 0 - repo_daysSinceLastCommit

| eval resource_id="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_id' . "/" . 'ssphp.resource.repo_id'

| fields ssphp.service.*,
         ssphp.resource.*,
         ssphp.score.*,
         repo_daysSinceLastCommit, repo_mostRecentCommitDate, repo_compliant


``` ##################### replace with dashboard filter ##################### ```

``` ##################### end dashboard query ##################### ```

| stats values(ssphp.service.*) as ssphp.service.*,
        values(ssphp.resource.*) as ssphp.resource.*, 
        count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator,
        values(ssphp.score.remediation_priority) as ssphp.score.remediation_priority

        by ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.repo_id
        
| eval ssphp.score.score=if('ssphp.score.numerator'>0,"0","100")


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id="ado_001_cis_1-2-7",
       ssphp.use_case.title=upper(mvindex(split('ssphp.use_case.id',"_"),0)) . " [CIS ".replace(mvindex(split('ssphp.use_case.id',"_"),-1),"-",".") . "]",
       ssphp.use_case.savedsearch="ssphp_use_case_" . 'ssphp.use_case.id' . "{{environment}}",
       ssphp.use_case.version.number="1.0.1",
       ssphp.use_case.version.last_changed_date="2025-08-01",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.group="ADO",
       ssphp.use_case.group_name="REPOS",
       ssphp.use_case.category="REPOS",
       ssphp.resource.id="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_id' . "/" . 'ssphp.resource.repo_id',
       ssphp.resource.name="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_name' . "/" . 'ssphp.resource.repo_name',
       ssphp.resource.type="ado/repo",
       ssphp.assessment.source="ADO"

| eval ssphp.risk.expectancy="-",
       ssphp.risk.impact="-"

| eval ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red"),
       ssphp.score.scoring_narrative="In order to be compliant, all repos MUST have had at least 1 commit in the past 180 days."

| eval SSPHP_UID=md5('ssphp.use_case.category'."|".'ssphp.service.product'."|".'ssphp.use_case.id'."|".'ssphp.resource.id')

`ssphp_use_case_add_cis_metadata_github{{environment}}`
| fields - ssphp.source.foundational_system

`ssphp_use_case_write{{environment}}`


"""
