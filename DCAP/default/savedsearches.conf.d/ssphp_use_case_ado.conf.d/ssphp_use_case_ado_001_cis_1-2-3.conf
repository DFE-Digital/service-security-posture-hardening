[ssphp_use_case_ado_001_cis_1-2-3{{environment}}]
cron_schedule = 10 6 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_use_case_ado_data_permissions_1{{environment}}`

`ssphp_use_case_ado_data_permissions_2{{environment}}("Delete or disable repository")`

`ssphp_use_case_ado_data_permissions_3{{environment}}("Git Repositories")`


| fields Actors, effectiveAllow_actions, effectiveDeny_actions, effectiveAllow_actions_actors, actors_with_effectiveAllow_actions, effectiveDeny_actions_actors, actors_with_effectiveDeny_actions,
         ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.project_name, ssphp.resource.repo_id, ssphp.resource.repo_name, tenant, repo_url,
         project_revision, project_state, project_url, project_visibility, 
         repo_daysSinceLastCommit, repo_default_branch, repo_isdisabled, repo_ismaintenance, repo_mostRecentCommitDate, 
         ssphp.service.*

| eval policy_compliant=if(mvcount(mvfilter(match('effectiveAllow_actions',"Delete or disable repository")))>0,"false","true")

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if('policy_compliant'="true",
                                            'ssphp.score.non_compliant_fields',
                                            mvappend('ssphp.score.non_compliant_fields',"actors_with_effectiveAllow_actions"))

| eval ssphp.score.remediation_priority=if('policy_compliant'="true",0,17)

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="1",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| eval resource_id="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_id' . "/" . 'ssphp.resource.repo_id'

| fields ssphp.service.*,
         ssphp.resource.*,
         ssphp.score.*,
         project_state, project_visibility, ssphp.resource.repo_name, ssphp.resource.repo_id, resource_id,
         Actors, effectiveAllow_actions, effectiveDeny_actions, effectiveAllow_actions_actors, actors_with_effectiveAllow_actions, effectiveDeny_actions_actors, actors_with_effectiveDeny_actions


``` ##################### replace with dashboard filter ##################### ```

``` ##################### end dashboard query ##################### ```

| stats values(ssphp.service.*) as ssphp.service.*,
        values(ssphp.resource.*) as ssphp.resource.*, 
        count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator,
        values(ssphp.score.remediation_priority) as ssphp.score.remediation_priority

        by ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.repo_id
        
| eval ssphp.score.score=if('ssphp.score.numerator'>0,"0","100")


``` ##################### add metadata ##################### ```

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id="ado_001_cis_1-2-3",
       ssphp.use_case.title=upper(mvindex(split('ssphp.use_case.id',"_"),0)) . " [CIS ".replace(mvindex(split('ssphp.use_case.id',"_"),-1),"-",".") . "]",
       ssphp.use_case.savedsearch="ssphp_use_case_" . 'ssphp.use_case.id' . "{{environment}}",
       ssphp.use_case.version.number="1.0.1",
       ssphp.use_case.version.last_changed_date="2025-08-01",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.group="ADO",
       ssphp.use_case.group_name="REPOS",
       ssphp.use_case.category="REPOS",
       ssphp.resource.id="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_id' . "/" . 'ssphp.resource.repo_id',
       ssphp.resource.name="/" . 'ssphp.resource.organization' . "/" . 'ssphp.resource.project_name' . "/" . 'ssphp.resource.repo_name',
       ssphp.resource.type="ado/repo",
       ssphp.assessment.source="ADO"

| eval ssphp.risk.expectancy="-",
       ssphp.risk.impact="-"

| eval ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red"),
       ssphp.score.scoring_narrative="In order to be compliant, repositories MUST NOT have an effective action (permission) associated that enables deletion of repositories."

| eval SSPHP_UID=md5('ssphp.use_case.category'."|".'ssphp.service.product'."|".'ssphp.use_case.id'."|".'ssphp.resource.id')

`ssphp_use_case_add_cis_metadata_github{{environment}}`
| fields - ssphp.source.foundational_system

`ssphp_use_case_write{{environment}}`


"""
