[ssphp_create_asset_inventory_index_azure{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

| inputlookup ssphp_bdmc.csv
| lookup local=true ssphp_bdmc_fbp.csv product_key

| rename resource_id as ssphp.resource.id,
         product_key as ssphp.service.product_key,
         tenantId as ssphp.resource.tenant,
         subscriptionId as ssphp.resource.subscription,
         subscription_name as ssphp.resource.subscription_name,
         resourceGroup as ssphp.resource.resource_group,
         resource_type as ssphp.resource.type,
         portfolio as ssphp.service.portfolio,
         service_line as ssphp.service.service_line,
         product as ssphp.service.product,
         product_clean as ssphp.service.product_clean,
         id as ssphp.service.id,
         cost_centre_code as ssphp.service.cost_centre.code,
         account_code as ssphp.service.cost_centre.account_code,
         activity_code as ssphp.service.cost_centre.activity_code,
         cost_centre_title as ssphp.service.cost_centre.title,
         cost_centre_owner as ssphp.service.contacts.cost_centre_owner,
         cost_centre_owner_email as ssphp.service.contacts.cost_centre_owner_email,
         financial_business_partner_email as ssphp.service.contacts.financial_business_partner_email,
         hosting_provider_email as ssphp.service.contacts.hosting_provider_email,
         hosting_support_email as ssphp.service.contacts.hosting_support_email,
         product_owner_email as ssphp.service.contacts.product_owner_email

| eval ssphp.resource.source="AZURE"

| fields ssphp.*

`ssphp_asset_inventory_write{{environment}}("azure")`

"""




[ssphp_create_asset_inventory_index_github{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="github" source="/orgs/*/properties/values"
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="github" source="/orgs/*/properties/values"
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| spath repository_name 
| spath repository_full_name 

| eval SSPHP_RUN=0 ``` create SSPHP_RUN first so it is at the start of the json object to avoid weird timestamp recognition problems```

| eval p=mvzip('properties{}.property_name','properties{}.value',"=")
| eval ssphp.service.portfolio=mvindex(split(mvfilter(match('p',"portfolio.*")),"="),1),
       ssphp.service.service_line=mvindex(split(mvfilter(match('p',"service_line.*")),"="),1),
       ssphp.service.product=mvindex(split(mvfilter(match('p',"product.*")),"="),1),
       ssphp.service.service_line=if('ssphp.service.portfolio'="null","Unassigned",'ssphp.service.service_line'),
       ssphp.service.product=if('ssphp.service.portfolio'="null","Unassigned",'ssphp.service.product'),
       ssphp.service.portfolio=if('ssphp.service.portfolio'="null","Unassigned",'ssphp.service.portfolio'),
       resource_type="github repo"

| rename ssphp.service.product as tags_product
`ssphp_clean_resource_product_tag_values{{environment}}`

| lookup local=true ssphp_bdmc_fbp.csv product_clean as tags_product_clean OUTPUT id, portfolio as ssphp.service.portfolio, service_line as ssphp.service.service_line, product as ssphp.service.product, account_code, activity_code, cost_centre_code, cost_centre_owner, cost_centre_title, financial_business_partner_email, hosting_provider_email, hosting_support_email, product_key, product_owner_email
| eval ssphp.service.cost_centre.code=if('ssphp.service.portfolio'="Unassigned","Unassigned",'cost_centre_code'),
       ssphp.service.id=if('ssphp.service.portfolio'="Unassigned","Unassigned",'id')

| fillnull value="Unassigned" ssphp.service.portfolio, ssphp.service.service_line, ssphp.service.product

| rename tags_product as ssphp.service.product, tags_product_clean as ssphp.service.product_clean

| lookup local=true ssphp_cost_centre_owner_emals.csv cost_centre_owner OUTPUT email as cost_centre_owner_email

| rename repository_id as ssphp.resource.id,
         repository_name as ssphp.resource.name,
         repository_full_name as ssphp.resource.full_name,
         product_key as ssphp.service.product_key,
         resource_type as ssphp.resource.type,
         
         account_code as ssphp.service.cost_centre.account_code,
         activity_code as ssphp.service.cost_centre.activity_code,
         cost_centre_title as ssphp.service.cost_centre.title,
         cost_centre_owner as ssphp.service.contacts.cost_centre_owner,
         cost_centre_owner_email as ssphp.service.contacts.cost_centre_owner_email,
         financial_business_partner_email as ssphp.service.contacts.financial_business_partner_email,
         hosting_provider_email as ssphp.service.contacts.hosting_provider_email,
         hosting_support_email as ssphp.service.contacts.hosting_support_email,
         product_owner_email as ssphp.service.contacts.product_owner_email

| eval ssphp.resource.source="GITHUB"

| fields SSPHP_RUN, ssphp.*

`ssphp_asset_inventory_write{{environment}}("github")`

"""




[ssphp_create_asset_inventory_index_ado{{environment}}]
cron_schedule = 55 7 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="ADO" source="repo_policy_joins:*" 
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ADO" source="repo_policy_joins:*"
    | stats max(SSPHP_RUN) as SSPHP_RUN 
    | return SSPHP_RUN] 
| rename repoId as repo_id, projectId as project_id, organization as organization, policyId as policy_id 
| fields organization, project_id, repo_id, policy_id 

| eval SSPHP_RUN=0 ``` create SSPHP_RUN first so it is at the start of the json object to avoid weird timestamp recognition problems```

| join type=outer organization, project_id, repo_id 
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ADO" source="*:https://dev.azure.com/*/*/_apis/git/repositories?api-version=*"
        [| search `ssphp_metrics_data_index{{environment}}` sourcetype="ADO" source="*:https://dev.azure.com/*/*/_apis/git/repositories?api-version=*"
        | stats max(SSPHP_RUN) as SSPHP_RUN 
        | return SSPHP_RUN]
    | spath metadata.type
    | search metadata.type="fn git_repository_list"
    
    | rename id as repo_id, name as repo_name, defaultBranch as repo_default_branch, isDisabled as repo_isdisabled, isInMaintenance as repo_ismaintenance, project.* as project_*, metadata.organization as organization, metadata.tenant as tenant, url as repo_url, daysSinceLastCommit as repo_daysSinceLastCommit, mostRecentCommitDate as repo_mostRecentCommitDate
    | fields tenant, organization, project_id, project_name, project_revision, project_state, project_url, project_visibility, repo_id, repo_name, repo_default_branch, repo_isdisabled, repo_ismaintenance, repo_url, repo_daysSinceLastCommit, repo_mostRecentCommitDate ] 

| rename organization as ssphp.resource.organization,
         project_id as ssphp.resource.project_id,
         project_name as ssphp.resource.project_name,
         repo_id as ssphp.resource.repo_id,
         repo_name as ssphp.resource.repo_name
         
| fields  SSPHP_RUN, ssphp.resource.organization,  ssphp.resource.project_id,  ssphp.resource.project_name,  ssphp.resource.repo_id,  ssphp.resource.repo_name

| eval  ssphp.service.id=case(like(lower('ssphp.resource.project_name'),"s163%"),771,
                              like(lower('ssphp.resource.project_name'),"s101%"),562,
                              1==1,"Unassigned")

| lookup local=true ssphp_bdmc_fbp.csv id as ssphp.service.id OUTPUT account_code as ssphp.service.cost_centre.account_code,
                                                                      activity_code as ssphp.service.cost_centre.activity_code,
                                                                      cost_centre_code as ssphp.service.cost_centre.code,
                                                                      cost_centre_owner as ssphp.service.contacts.cost_centre_owner,
                                                                      cost_centre_title as ssphp.service.cost_centre.title,
                                                                      financial_business_partner_email as ssphp.service.contacts.financial_business_partner_email,
                                                                      hosting_provider_email as ssphp.service.contacts.hosting_provider_email,
                                                                      hosting_support_email as ssphp.service.contacts.hosting_support_email,
                                                                      portfolio as ssphp.service.portfolio,
                                                                      product as ssphp.service.product,
                                                                      product_clean as ssphp.service.product_clean,
                                                                      product_key as ssphp.service.product_key,
                                                                      product_owner_email as ssphp.service.contacts.product_owner_email,
                                                                      service_line as ssphp.service.service_line
| fillnull value="Unassigned" ssphp.service.portfolio, ssphp.service.service_line, ssphp.service.product

| eval ssphp.resource.source="ADO"

| fields SSPHP_RUN, ssphp.*
| stats values(*) as * by ssphp.resource.organization, ssphp.resource.project_id, ssphp.resource.repo_id

`ssphp_asset_inventory_write{{environment}}("ado")`

"""

