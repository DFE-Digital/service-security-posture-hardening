[ssphp_use_case_azure_002_cis_2-1-18{{environment}}]
cron_schedule = 30 6 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| `ssphp_use_case_azure_get_config_data{{environment}}("Microsoft.Security/securityContacts")`

| fields id, properties.alertNotifications.state, properties.notificationsByRole.state, properties.notificationsByRole.roles{}

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if('properties.alertNotifications.state'="On",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"properties.alertNotifications.state")),
       ssphp.score.non_compliant_fields=if('properties.notificationsByRole.state'="On",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"properties.notificationsByRole.state")),
       ssphp.score.non_compliant_fields=if(like('properties.notificationsByRole.roles{}',"%Owner%"),
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"properties.notificationsByRole.roles{}"))

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="3",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| join type=outer id
    [| search 'ssphp_metrics_data_index{{environment}}' sourcetype="azure_resource_graph" type="microsoft.resources/subscriptions"
         [| search index=ssphp_metrics_data sourcetype="azure_resource_graph" type="microsoft.resources/subscriptions"
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
     | stats values(tenantId) as tenantId by subscriptionId
     | eval id="/subscriptions/".'subscriptionId'."/providers/Microsoft.Security/securityContacts/default"
     | table subscriptionId, id, tenantId]

| fields tenantId, id, properties.alertNotifications.state, properties.notificationsByRole.state, properties.notificationsByRole.roles{}, ssphp.score.score, ssphp.score.numerator, ssphp.score.denominator, ssphp.score.non_compliant_fields

``` ##################### end dashboard query ##################### ```


| stats count as ssphp.score.denominator, 
        sum(eval(if('ssphp.score.score'=100,0,1))) as ssphp.score.numerator
| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

`ssphp_use_case_write_null_output_override{{environment}}(0)`


``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now()),
       ssphp.use_case.id="azure_002_cis_2-1-18",

       ssphp.use_case.version.number="4.0.2",
       ssphp.use_case.version.last_changed_date="2024-12-16",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       
       ssphp.microsoft.description=coalesce('description',"-"),
       ssphp.microsoft.implementation_status=coalesce('implementationStatus',"-")

`ssphp_use_case_add_cis_metadata_azure{{environment}}`

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5",
       ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-"

| eval ssphp.source.service="AZURE",
       ssphp.source.service_name="AZURE",
       ssphp.use_case.savedsearch="ssphp_use_case_azure_002_cis_2-1-18{{environment}}"


| eval ssphp.score.scoring_narrative="In order to be compliant, the field values must be as follows :~~
properties.alertNotifications.state = 'On'~~
properties.notificationsByRole.state = 'On'~~
properties.notificationsByRole.roles{} includes the value 'Owner'"

| eval ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")


`ssphp_use_case_write{{environment}}`

"""
