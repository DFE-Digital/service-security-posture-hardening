[ssphp_use_case_vulnerabilities_dfe_1-0{{environment}}]
cron_schedule = 30 3 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """
| loadjob savedsearch="{{splunk_user}}:{{app}}{{environment}}:ssphp_vulnerabilities_create_data{{environment}}"
| where ```SEVERITY>=5 AND``` VULNERABILITY_AGE>=30

| lookup qualys_kb_lookup QID OUTPUT TITLE, DIAGNOSIS, SOLUTION, CONSEQUENCE, CVE
```| search HOSTNAME="das*"```

| eval HOSTNAME=lower('HOSTNAME')
| join type=outer HOSTNAME
    [|  search `ssphp_metrics_data_index{{environment}}` sourcetype=azure_resource_graph type="microsoft.compute/virtualmachines" earliest=-1d@d latest=now
    | eval HOSTNAME=lower('name')
| table HOSTNAME, tenantId, subscriptionId, resourceGroup]


| eval ssphp_id=lower("/tenancies/".'tenantId'."/subscriptions/".'subscriptionId'."/resourcegroups/".'resourceGroup') 
| lookup ssphp_resourcegroup_to_ssphp_service_tag{{environment}}.csv ssphp_id OUTPUT 
                        ssphp_service_id as ssphp.service.id
                        ssphp_service_name as ssphp.service.name
                        ssphp_service_display_name as ssphp.service.display_name
                        ssphp_service_group as ssphp.service.group
                        ssphp_service_division as ssphp.service.division
                        ssphp_service_portfolio as ssphp.service.portfolio
                        
| sort 0 - VULNERABILITY_AGE

| fields HOSTNAME, ownership, QID, TITLE, SEVERITY, VULNERABILITY_AGE, CVE, FIRST_FOUND_DATETIME, LAST_VM_SCANNED_DATE, DIAGNOSIS, CONSEQUENCE, SOLUTION,
         ssphp.service.id, ssphp.service.name, ssphp.service.display_name, ssphp.service.group, ssphp.service.division, ssphp.service.portfolio, ssphp_id
                        
| where isnotnull('ssphp.service.id')

| rename QID as ssphp.use_case.id

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.title="Qualys Vulnerability Assessment [".'ssphp.use_case.id'."]",
       ssphp.use_case.savedsearch="ssphp_use_case_vulnerabilities_dfe_1-0{{environment}}",
       ssphp.use_case.group="AZURE",
       ssphp.use_case.group_name="QUALYS",
       ssphp.use_case.version.number="3.0.0",
       ssphp.use_case.version.last_changed_date="2024-07-31",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.category="VULNERABILITY"
       
| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.ciso_priority=case('SEVERITY'=5,"1",
                                      'SEVERITY'>2,"2",
                                      1==1,"3"),
       ssphp.score.scoring_narrative="DUNNO",
       ssphp.resource.type="microsoft.compute/virtualmachines"

|  rename TITLE as ssphp.assessment.display_name,
          FIRST_FOUND_DATETIME as ssphp.assessment.firstEvaluationDate,
          LAST_VM_SCANNED_DATE as ssphp.assessment.statusChangeDate
          
|  rename DIAGNOSIS as ssphp.assessment.diagnosis, CONSEQUENCE as ssphp.assessment.consequence, SOLUTION as ssphp.assessment.solution, CVE as ssphp.assessment.cve,
          ssphp_id as ssphp.resource.id, HOSTNAME as ssphp.resource.name

| fields SSPHP_RUN, SEVERITY, VULNERABILITY_AGE, ssphp.use_case.id, ssphp.score.ciso_priority, 
        ssphp.*
        
``` ##################### end dashboard query ##################### ```


| eval ssphp.score.score=case('ssphp.score.ciso_priority'=1 AND 'VULNERABILITY_AGE'>10,0,
                                  'ssphp.score.ciso_priority'=2 AND 'VULNERABILITY_AGE'>60,0,
                                  'ssphp.score.ciso_priority'=3 AND 'VULNERABILITY_AGE'>90,0,
                                  1==1,100),
       ssphp.score.denominator="-",
       ssphp.score.numerator="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

| fields SSPHP_RUN, ssphp.score.score, SEVERITY, VULNERABILITY_AGE, ssphp.use_case.id, ssphp.score.ciso_priority, 
        ssphp.*
        
        | eval ssphp.score.color=case('ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="1","red",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="2","orange",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="3","white",
                              1==1,'ssphp.score.color')


`ssphp_use_case_write{{environment}}`

"""
