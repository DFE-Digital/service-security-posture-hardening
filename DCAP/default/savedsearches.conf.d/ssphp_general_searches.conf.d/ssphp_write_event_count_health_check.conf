[ssphp_write_event_count_health_check]
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 0
search = """

| inputlookup ssphp_data_dependencies_by_use_case.csv where (tech_test="AAD" OR tech_test="ADO" OR tech_test="AWS" OR tech_test="AZURE" OR tech_test="GITHUB" OR tech_test="M365" ```OR tech_test="QUALYS"```)
| fillnull value="*" index, sourcetype, source

| stats values(savedsearch) as savedsearch, values(tech_test) as tech_test by index, sourcetype, source

| search index!="aws_cloudtrail"

| map
    [| tstats count where index="$index$" sourcetype="$sourcetype$" source="$source$" earliest=-11d@d latest=now groupby index, sourcetype, source, _time span=1d 
              | eval time=if(strftime('_time',"%Y-%m-%d")=strftime(now(),"%Y-%m-%d"),"TODAY",strftime('_time',"%Y-%m-%d")),
                     date_{time}='count',
                     in_tech_test="$tech_test$",
                     in_savedsearch="$savedsearch$",
                     in_index="$index$",
                     in_sourcetype="$sourcetype$",
                     in_source="$source$",
                     in_type=""
              | fields - _time, time, count
              | stats values(*) as * by index, sourcetype, source, in_tech_test
              | eval in_tech_test=split('in_tech_test'," ")
              | mvexpand in_tech_test
    ] maxsearches=999

| append 
    [| search index="ssphp_metrics_data" sourcetype="azure_resource_graph" type="microsoft.security/assessments" earliest=-11d@d latest=now
              | eval day=strftime(_time,"%Y-%m-%d")
              | stats count by index, sourcetype, source, day
              
              | eval time=if('day'=strftime(now(),"%Y-%m-%d"),"TODAY",'day'),
                     date_{time}='count',
                     in_index="ssphp_metrics_data",
                     in_sourcetype="azure_resource_graph",
                     in_source="*",
                     in_type="microsoft.security/assessments",
                     in_tech_test="DEFENDER",
                     in_savedsearch="ssphp_use_case_defender_assessments ssphp_use_case_defender_assessments_k8s"
              | fields - _time, time, count, day
              | stats values(*) as * by in_index, in_sourcetype, in_source, in_tech_test
    ]

| append 
    [|  search index="qualys" sourcetype="qualys:hostDetection" earliest=-11d@d latest=@d
              | eval day=strftime(_time,"%Y-%m-%d")
              | stats count by index, sourcetype, source, day
              
              | eval time=if('day'=strftime(now()-86400,"%Y-%m-%d"),"YESTERDAY",'day'),
                             date_{time}='count',
                             in_index="qualys",
                             in_sourcetype="qualys:hostDetection",
                             in_source="*",
                             in_type="*",
                             in_tech_test="QUALYS",
                             in_savedsearch="ssphp_use_case_vulnerabilities_dfe_1-0"
              | fields - _time, time, count, day
              | stats values(*) as * by in_index, in_sourcetype, in_source, in_tech_test

              | rename date_YESTERDAY as date_TODAY ``` *** This control is looking at the number of qualys events yesterday, not today, since the volume builds up incrementally during the day *** need to use variable called today to make the search generic```
    ]

| append 
    [|  search index="aws_cloudtrail" sourcetype="aws:cloudtrail" earliest=-11d@d latest=@d

     | eval day=strftime(_time,"%Y-%m-%d")
     | stats count by index, sourcetype, day
              
     | eval time=if('day'=strftime(now()-86400,"%Y-%m-%d"),"YESTERDAY",'day'),
                    date_{time}='count',
                    in_index="aws_cloudtrail",
                    in_sourcetype="aws:cloudtrail",
                    in_source="*",
                    in_type="*",
                    in_tech_test="AWS",
                    in_savedsearch="ssphp_use_case_dns_dfe_3-0"
     | fields - _time, time, count, day
     | stats values(*) as * by in_index, in_sourcetype, in_source, in_tech_test

     | rename date_YESTERDAY as date_TODAY ``` *** This control is looking at the number of cloudtrail events yesterday, not today, since the volume builds up incrementally during the day *** need to use variable called today to make the search generic```
    ]


| foreach date_* [| eval "<<FIELD>>"=coalesce('<<FIELD>>',"0")]
| eval in_savedsearch=split('in_savedsearch'," ")

| stats sum(date_*) as date_*, values(in_savedsearch) as in_savedsearch by in_index, in_sourcetype, in_source, in_type, in_tech_test
| rename in_* as *, date_TODAY as TODAY


| eval stats.max=0, stats.min=999999999, stats.total=0, stats.count=0
| foreach date_* [| eval stats.count='stats.count'+1, stats.total='stats.total'+'<<FIELD>>', stats.max=if('<<FIELD>>'>'stats.max','<<FIELD>>','stats.max'), stats.min=if('stats.min'>0 AND '<<FIELD>>'<'stats.min','<<FIELD>>','stats.min')]
| eval stats.avg=round('stats.total'/'stats.count')
| rename TODAY as event_count.TODAY, date_* as event_count.date_*, index as data_origin.index, sourcetype as data_origin.sourcetype, source as data_origin.source, type as data_origin.type

| eval status.error=case('event_count.TODAY'=0, "zero event count today",
                         'event_count.TODAY'>('stats.avg'*2),"today is more than 2x the average number of events",
                         'event_count.TODAY'<('stats.avg'/2), "today is less than half the average number of events",
                         'event_count.TODAY'>('stats.max'*2), "today is more than 2x the maximum number of events",
                         'event_count.TODAY'<('stats.min'/2), "today is less than half the minimum number of events",
                         1==1, "-"),
       status.health=if('status.error'="-","healthy","unhealthy"),
       SSPHP_RUN=now()



| makejson output=_raw

| eval _time=SSPHP_RUN
| fields _time, _raw 
| collect output_format=hec testmode=f addinfo=f index="ssphp_metrics_data_health"

"""
