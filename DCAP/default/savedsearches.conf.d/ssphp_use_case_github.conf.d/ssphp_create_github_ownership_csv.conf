[ssphp_create_github_ownership_csv{{environment}}]

dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 0
search = """

index="ssphp_metrics_data" sourcetype="github" source="*/collaborators" (permissions.admin="true" OR permissions.maintain="true" OR permissions.push="true") earliest=@d latest=now
| rex field=source "\/repos(?<repo_name>\/[^\/]*\/[^\/]*)\/" 

| join type=outer repo_name 
    [| search index="ssphp_metrics_data" sourcetype="github" source="github:*" earliest=@d latest=now
     | rex field=source "\/repos(?<full_repo_name>\/[^\/]*\/[^\/]*)\/" 
     | eval repo_name="/".'full_name' 
     | table repo_name, description, archived, html_url, created_at updated_at visibility, default_branch] 
    
| fields repo_name, description, archived, login, created_at, updated_at, html_url, visibility, default_branch


``` ********** vvv this is the DCAP macro ssphp_use_case_add_service_metadata_github vvv with the lookup for last SSPHP_RUN replaced vvvv *************** ```
| eval target_url=lower('repo_name')

| join type=outer target_url
    [| search index="ssphp_asset_inventory" ssphp.resource.source="GITHUB" earliest=@d latest=now
     | eval target_url="/".lower('ssphp.resource.full_name')
     | table target_url, ssphp.*]
| fields - target_url

| eval ssphp.service.portfolio=if(isnull('ssphp.service.portfolio'),"Unassigned",'ssphp.service.portfolio'),
       ssphp.service.service_line=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.service_line'),
       ssphp.service.product=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.product'),
       ssphp.service.cost_centre.code=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.cost_centre.code')

`ssphp_add_service_rolls_up_to_metadata`
``` ********** ^^^ this is the DCAP macro ssphp_use_case_add_service_metadata_github ^^^ with the lookup for last SSPHP_RUN replaced ^^^ *************** ```


| eval repo_status=if('ssphp.service.portfolio'="Unassigned" OR 'ssphp.service.service_line'="Unassigned" OR 'ssphp.service.product'="Unassigned" OR isnull('ssphp.service.portfolio') OR isnull('ssphp.service.service_line') OR isnull('ssphp.service.product') OR 'ssphp.service.portfolio'="" OR 'ssphp.service.service_line'="" OR 'ssphp.service.product'="", "Untagged", "Tagged") 

| eval organisation=mvindex(split('repo_name',"/"),1),
    repo_name=mvindex(split('repo_name',"/"),2) 
| lookup ssphp_github_names_active.csv Username as login OUTPUT Email 

| eval Email=mvsort(mvdedup(Email)),
       Email=if(Email IN ("dev.ops@education.gov.uk", "erhan.tahir@education.gov.uk", "jem.mccrory@education.gov.uk", "kate.nicolson@education.gov.uk"),"NO CONTRIBUTORS",'Email') 
| lookup ssphp_user_reporting_structure.csv user as Email OUTPUT rolls_up_to AS rolls_up_to_manager 

| fields organisation, repo_name, login, Email, rolls_up_to_manager, repo_status, description, archived, created_at, updated_at, html_url, visibility, default_branch, ssphp.service.*

| fillnull value="-" Email 
| eval contributor='login' . "," . 'Email' 

| stats values(contributor) as contributors,
        values(repo_status) as repo_status, 
        values(ssphp.service.*) as ssphp.service.*, 
        values(rolls_up_to_manager) as rolls_up_to_manager, 
        values(description) as description, 
        values(archived) as archived, 
        values(visibility) as visibility,
        values(created_at) as created_at,
        values(updated_at) as updated_at,
        values(html_url) as html_url, 
        values(default_branch) as default_branch
        by organisation, repo_name

| eval dfe_centrally_owned=if(mvcount(contributors)=mvcount(mvfilter(match('contributors',".*,NO CONTRIBUTORS$"))),"true","false")
| eval contributors=if(like('contributors',"%NO CONTRIBUTORS%") AND mvcount(contributors)=1,"NO CONTRIBUTORS",mvfilter(NOT match('contributors',"NO CONTRIBUTORS")))
       
```| search contributors!="NO CONTRIBUTORS" AND archived="false" ```

| fields dfe_centrally_owned, organisation, repo_name, description, repo_status, archived, visibility, html_url, default_branch, contributors, rolls_up_to_manager, created_at, updated_at, ssphp.service.*

| rename created_at AS created, updated_at AS last_updated html_url as URL, organisation as owner
 
| makemv contributors_github_login 
| makemv contributors_dfe_email 
| foreach mode=multivalue contributors 
          [| eval contributors_github_login=mvappend('contributors_github_login',mvindex(split(<<ITEM>>,","),0)), contributors_dfe_email=mvappend('contributors_dfe_email',mvindex(split(<<ITEM>>,","),1))]
          
| eval contributors_github_login=if(mvcount('contributors') > 1, 'contributors_github_login', mvindex(split('contributors',","),0)), 
       contributors_dfe_email=if(mvcount('contributors') > 1, 'contributors_dfe_email', mvindex(split('contributors',","),1)),
       created=strftime(strptime('created',"%Y-%m-%dT%H:%M:%SZ"),"%Y-%m-%d %H:%M:%S"),
       last_updated=strftime(strptime('last_updated',"%Y-%m-%dT%H:%M:%SZ"),"%Y-%m-%d %H:%M:%S")
    
| table  owner, repo_name, repo_status, created, last_updated, dfe_centrally_owned, archived, visibility, default_branch```, contributors```, contributors_github_login, contributors_dfe_email, rolls_up_to_manager, URL, ssphp.service.portfolio, ssphp.service.service_line, ssphp.service.product, ssphp.service.*

| outputlookup createinapp=true append=false ssphp_github_repo_ownership_details.csv


"""
