[ssphp_create_github_ownership_csv{{environment}}]

dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 0
search = """

index="ssphp_metrics_data" sourcetype="github" source="github:*" earliest=@d latest=now

| eval key=lower(full_name)
| fields key, full_name, repo_name, description, archived, html_url, created_at updated_at visibility, default_branch

| join type=outer key limits max=0
    [| search index="ssphp_metrics_data" sourcetype="github" source="*/collaborators" (permissions.admin="true" OR permissions.maintain="true" OR permissions.push="true") earliest=@d latest=now
     | rex field=source "\/repos\/(?<full_name>[^\/]+\/[^\/]+)\/" 
     | eval key=lower(full_name)
     | lookup ssphp_github_names_active.csv Username as login OUTPUT Email
     | eval Email=mvsort(mvdedup('Email'))
     | fillnull value="-" Email
     | lookup ssphp_user_reporting_structure.csv user as Email OUTPUT rolls_up_to AS rolls_up_to_manager 
     | eval login='login'."~~".'Email'."~~".rolls_up_to_manager
     | table key, login]

| fields key, full_name, repo_name, description, login, archived, html_url, created_at updated_at visibility, default_branch

| stats values(login) as contributors, values(*) as * by key
| fillnull value="-" contributors

| eval organisation=mvindex(split('full_name',"/"),0),
       repo_name=mvindex(split('full_name',"/"),1)

| fields key, full_name, organisation, repo_name, description, contributors, archived, html_url, created_at updated_at visibility, default_branch


``` ********** ADD OWNNERSHIP FROM SSPHP ASSET REGISTRY *************** ```
| join type=outer key
    [| search index="ssphp_asset_inventory" ssphp.resource.source="GITHUB" earliest=@d latest=now
     | eval key=lower('ssphp.resource.full_name')
     | table key, ssphp.*]

| eval ssphp.service.portfolio=if(isnull('ssphp.service.portfolio'),"Unassigned",'ssphp.service.portfolio'),
       ssphp.service.service_line=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.service_line'),
       ssphp.service.product=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.product'),
       ssphp.service.cost_centre.code=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.cost_centre.code')

`ssphp_add_service_rolls_up_to_metadata`

| eval repo_status=if('ssphp.service.portfolio'="Unassigned" OR 'ssphp.service.service_line'="Unassigned" OR 'ssphp.service.product'="Unassigned" OR isnull('ssphp.service.portfolio') OR isnull('ssphp.service.service_line') OR isnull('ssphp.service.product') OR 'ssphp.service.portfolio'="" OR 'ssphp.service.service_line'="" OR 'ssphp.service.product'="", "Untagged", "Tagged")

| fields organisation, repo_name, contributors, repo_status, description, archived, created_at, updated_at, html_url, visibility, default_branch, ssphp.service.*

| eval non_central_owners=mvfilter(NOT like('contributors',"%kate.nicolson@education.gov.uk%") AND NOT like('contributors',"%erhan.tahir@education.gov.uk%") AND NOT like('contributors',"%dev.ops@education.gov.uk%") AND NOT like('contributors',"%jem.mccrory@education.gov.uk%")),
       dfe_centrally_owned=case('non_central_owners'="-","false",
                                mvcount(contributors)>0 AND isnull('non_central_owners'),"true",
                                1==1,"false"),
       contributors=if('dfe_centrally_owned'="false",'non_central_owners',null()),
       created=strftime(strptime('created_at',"%Y-%m-%dT%H:%M:%SZ"),"%Y-%m-%d %H:%M:%S"),
       last_updated=strftime(strptime('updated_at',"%Y-%m-%dT%H:%M:%SZ"),"%Y-%m-%d %H:%M:%S")

| fields organisation, repo_name, contributors, dfe_centrally_owned, repo_status, description, archived, created, last_updated, html_url, visibility, default_branch, ssphp.service.*

| makemv contributors_github_login  | makemv contributors_dfe_email  | makemv rolls_up_to_manager
| foreach mode=multivalue contributors 
          [| eval contributors_github_login=mvappend('contributors_github_login',mvindex(split(<<ITEM>>,"~~"),0)), contributors_dfe_email=mvappend('contributors_dfe_email',mvindex(split(<<ITEM>>,"~~"),1)), rolls_up_to_manager=mvappend('rolls_up_to_manager',mvindex(split(<<ITEM>>,"~~"),2))]
| eval rolls_up_to_manager=mvsort(mvdedup('rolls_up_to_manager'))

| table organisation, repo_name, description, contributors_github_login, contributors_dfe_email, rolls_up_to_manager, dfe_centrally_owned, repo_status, archived, created, last_updated, html_url, visibility, default_branch, ssphp.service.*

| outputlookup createinapp=true append=false ssphp_github_repo_ownership_details.csv


"""
