[ssphp_use_case_github_001_cis_1-1-7{{environment}}]
cron_schedule = 55 5 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

| `ssphp_use_case_github_DEV("*/branches/*/protection")`

| rename required_pull_request_reviews.require_code_owner_reviews as require_code_owner_reviews
| fields full_repo_name, url, repo, ssphp_http_status, require_code_owner_reviews

`ssphp_use_case_add_service_metadata_github_DEV` 

| fillnull value="false" require_code_owner_reviews
| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if(('ssphp_http_status'!="200" OR
                                                 lower('require_code_owner_reviews')!="true" OR
                                                 isnull('ssphp_http_status')),
                                            mvappend('ssphp.score.non_compliant_fields',"require_code_owner_reviews"),
                                            'ssphp.score.non_compliant_fields')

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="1",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields ssphp.service.id, ssphp.service.name, ssphp.service.display_name, ssphp.service.group, ssphp.service.portfolio, ssphp.service.division,
         full_repo_name, ssphp_http_status, require_code_owner_reviews, ssphp.score.non_compliant_fields, ssphp.score.score


``` ##################### end dashboard query ##################### ```
| stats count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator,
        values(ssphp.service.name) as ssphp.service.name,
        values(ssphp.service.display_name) as ssphp.service.display_name,
        values(ssphp.service.group) as ssphp.service.group,
        values(ssphp.service.portfolio) as ssphp.service.portfolio,
        values(ssphp.service.division) as ssphp.service.division
        by ssphp.service.id
| eval ssphp.score.score=if('ssphp.score.numerator'>0,"0","100")
       

``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id="github_001_cis_1-1-7",
       ssphp.use_case.title=upper(substr(mvindex(split('ssphp.use_case.id',"_"),0),1,1)).substr(mvindex(split('ssphp.use_case.id',"_"),0),2) . " [CIS ".replace(mvindex(split('ssphp.use_case.id',"_"),-1),"-",".") . "]",
       ssphp.use_case.savedsearch="ssphp_use_case_" . 'ssphp.use_case.id' . "{{environment}}",
       ssphp.use_case.version.number="1.0.0",
       ssphp.use_case.version.last_changed_date="2024-06-20",
       ssphp.use_case.version.last_changed_by="Sam Pritchard",
       ssphp.use_case.group="GITHUB",
       ssphp.use_case.group_name="SDLC"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red"),
       ssphp.score.scoring_narrative="In order to be compliant, repositories must have branch protection on and the required code owner reviews set to true."

| search ssphp.service.id!="-"

`ssphp_use_case_add_cis_metadata_github{{environment}}`

`ssphp_use_case_write{{environment}}`

"""
