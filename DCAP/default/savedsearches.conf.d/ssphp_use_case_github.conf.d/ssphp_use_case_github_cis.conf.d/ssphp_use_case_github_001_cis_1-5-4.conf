###################### SERVICE VERSION ######################
[ssphp_use_case_github_001_cis_1-5-4_svc{{environment}}]
cron_schedule = 55 5 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

| `ssphp_use_case_github{{environment}}("/repos/*code-scanning*/default-setup")`
| eval repo_name="/".mvindex(split('source',"/"),2)."/".mvindex(split('source',"/"),3)

| eval state=if('ssphp_http_status'="403","not-configured",'state'),
       schedule=if('ssphp_http_status'="403","null",'schedule'),
       query_suite=if('ssphp_http_status'="403","-",'query_suite')

| fields repo_name, state, ssphp_http_status, schedule, query_suite

`ssphp_use_case_add_service_metadata_github{{environment}}` 

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if('state'!="not-configured",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"state")),
       ssphp.score.non_compliant_fields=if('ssphp_http_status'="200",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"ssphp_http_status")),
       ssphp.score.non_compliant_fields=if('schedule'="weekly",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"schedule"))

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="3",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields ssphp.service.id, ssphp.service.name, ssphp.service.display_name, ssphp.service.group, ssphp.service.portfolio, ssphp.service.division,
         repo_name, state, ssphp_http_status, schedule, query_suite,
         ssphp.score.score, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```
| stats count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator,
        values(ssphp.service.name) as ssphp.service.name,
        values(ssphp.service.display_name) as ssphp.service.display_name,
        values(ssphp.service.group) as ssphp.service.group,
        values(ssphp.service.portfolio) as ssphp.service.portfolio,
        values(ssphp.service.division) as ssphp.service.division
        by ssphp.service.id, repo_name
        
| eval ssphp.score.score=if('ssphp.score.numerator'>0,"0","100")
       

``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id="github_001_cis_1-5-4",
       ssphp.use_case.title=upper(substr(mvindex(split('ssphp.use_case.id',"_"),0),1,1)).substr(mvindex(split('ssphp.use_case.id',"_"),0),2) . " [CIS ".replace(mvindex(split('ssphp.use_case.id',"_"),-1),"-",".") . "]",
       ssphp.use_case.savedsearch="ssphp_use_case_" . 'ssphp.use_case.id' . "_svc{{environment}}",
       ssphp.use_case.version.number="1.0.1",
       ssphp.use_case.version.last_changed_date="2024-07-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.group="GITHUB",
       ssphp.use_case.group_name="REPOS",
       ssphp.use_case.category="REPOS",
       ssphp.resource.id='repo_name',
       ssphp.resource.name='repo_name',
       ssphp.resource.type="github/repo"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red"),
       ssphp.score.scoring_narrative="In order to be compliant :
       state must be 'configured'
       ssphp_http_status must be '200'
       schedule must be 'weekly' for the repo."

| search ssphp.service.id!="-"

`ssphp_use_case_add_cis_metadata_github{{environment}}`

`ssphp_use_case_write{{environment}}`

"""


###################### FOUNDATIONAL SYSTEM VERSION ######################
[ssphp_use_case_github_001_cis_1-5-4_fs{{environment}}]
cron_schedule = 55 5 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

| `ssphp_use_case_github{{environment}}("/repos/*code-scanning*/default-setup")`
| eval repo_name="/".mvindex(split('source',"/"),2)."/".mvindex(split('source',"/"),3)

| eval state=if('ssphp_http_status'="403","not-configured",'state'),
       schedule=if('ssphp_http_status'="403","null",'schedule'),
       query_suite=if('ssphp_http_status'="403","-",'query_suite')

| fields repo_name, state, ssphp_http_status, schedule, query_suite

| makemv ssphp.score.non_compliant_fields
| eval ssphp.score.non_compliant_fields=if('state'!="not-configured",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"state")),
       ssphp.score.non_compliant_fields=if('ssphp_http_status'="200",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"ssphp_http_status")),
       ssphp.score.non_compliant_fields=if('schedule'="weekly",
                                           'ssphp.score.non_compliant_fields',
                                           mvappend('ssphp.score.non_compliant_fields',"schedule"))

| eval ssphp.score.numerator=coalesce(mvcount('ssphp.score.non_compliant_fields'),"0"),
       ssphp.score.denominator="3",
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')

| fields repo_name, state, ssphp_http_status, schedule, query_suite,
         ssphp.score.score, ssphp.score.non_compliant_fields


``` ##################### end dashboard query ##################### ```
| stats count as ssphp.score.denominator,
        sum(eval(if('ssphp.score.score'<100,1,0))) as ssphp.score.numerator

| eval ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator')



``` ##################### add metadata ##################### ```
| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id="github_001_cis_1-5-4",
       ssphp.use_case.title=upper(substr(mvindex(split('ssphp.use_case.id',"_"),0),1,1)).substr(mvindex(split('ssphp.use_case.id',"_"),0),2) . " [CIS ".replace(mvindex(split('ssphp.use_case.id',"_"),-1),"-",".") . "]",
       ssphp.use_case.savedsearch="ssphp_use_case_" . 'ssphp.use_case.id' . "_fs{{environment}}",
       ssphp.use_case.version.number="1.0.0",
       ssphp.use_case.version.last_changed_date="2024-07-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.source.service="GITHUB",
       ssphp.source.service_name="REPOS",
       ssphp.use_case.notes=""

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.ciso_priority="1",
       ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red"),
       ssphp.score.scoring_narrative="In order to be compliant :
       state must be 'configured'
       ssphp_http_status must be '200'
       schedule must be 'weekly' for the repo."

`ssphp_use_case_add_cis_metadata_github{{environment}}`

`ssphp_use_case_write{{environment}}`

"""
