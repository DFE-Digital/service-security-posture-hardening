[ssphp_use_case_defender_assessments{{environment}}]
cron_schedule = 30 3 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments*" earliest=-7d@d latest=now

    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments*" earliest=-7d@d latest=now
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

`ssphp_add_service_metadata{{environment}}`

| where isnotnull('ssphp.service.id')

| regex id="^\/subscriptions\/.*\/"
| fields id, ssphp_azure_tenant_id, ssphp_azure_subscription_id, ssphp_azure_resource_group_id, 
         ssphp.service.id, ssphp.service.name, ssphp.service.display.name, ssphp.service.group, ssphp.service.division, ssphp.service.portfolio, 
         tags, location, name, type, properties.resourceDetails.ResourceType, properties.resourceDetails.ResourceId, properties.resourceDetails.ResourceName, properties.displayName, 
         properties.metadata.displayName, properties.metadata.categories{}, properties.metadata.remediationDescription, properties.metadata.severity, properties.metadata.threats{}, properties.metadata.tactics{}, properties.metadata.techniques{}, 
         properties.status.code, properties.metadata.description, properties.status.firstEvaluationDate, properties.status.statusChangeDate

| rex field=id "[\s\S]*\/(?<ssphp_use_case_id>[\s\S]*)$"
| rename ssphp_use_case_id as ssphp.use_case.id

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.title="Microsoft Defender Assessment [".'ssphp.use_case.id'."]",
       ssphp.use_case.savedsearch="ssphp_use_case_defender_assessments{{environment}}",
       ssphp.use_case.group="AZURE",
       ssphp.use_case.group_name="MS-DEFENDER",
       ssphp.use_case.version.number="2.0.1",
       ssphp.use_case.version.last_changed_date="2024-07-31",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.category="POSTURE"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.ciso_priority=case(lower('properties.metadata.severity')="high","1",
                                      lower('properties.metadata.severity')="medium","2",
                                      1==1,"3"),
       ssphp.score.scoring_narrative="In order to be compliant, the value of properties.status.code MUST be 'Healthy' for each of the Resources listed.
Note : the health status relates only to the Assessment checked for this specific Control : 'xxxxxxxxxx'.
All the resources marked as 'Unhealthy' must be remediated"


| fields SSPHP_RUN, ssphp.use_case.id, id, type, 
        ssphp.service.id, ssphp.service.name, ssphp.service.display.name, ssphp.service.group, ssphp.service.division, ssphp.service.portfolio, 
        ssphp_azure_tenant_id, ssphp_azure_subscription_id, ssphp_azure_resource_group_id, 
        properties.resourceDetails.ResourceType, properties.resourceDetails.ResourceId, properties.resourceDetails.ResourceName,
        properties.displayName, properties.metadata.categories{}, properties.metadata.displayName, properties.metadata.remediationDescription, properties.metadata.severity, properties.metadata.tactics{}, properties.metadata.techniques{}, properties.metadata.threats{}, properties.status.code, properties.metadata.description, properties.status.firstEvaluationDate, properties.status.statusChangeDate, 
        ssphp.*

``` ##################### end dashboard query ##################### ```

| eval ssphp.score.numerator=coalesce(mvcount(mvfilter(match('properties.status.code',"Unhealthy"))),"0"),
       ssphp.score.denominator=coalesce(mvcount('properties.status.code'),"0"),
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator'),
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

| rename ssphp_azure_* as ssphp.resource.*,
         properties.resourceDetails.ResourceType as ssphp.resource.type,
         properties.resourceDetails.ResourceId as ssphp.resource.id,
         properties.resourceDetails.ResourceName as ssphp.resource.name,
         id as ssphp.assessment.id,
         properties.metadata.* as ssphp.assessment.*,
         properties.* as ssphp.assessment.*,
         ssphp.assessment.status.code as ssphp.assessment.status_code,
         ssphp.assessment.displayName as ssphp.assessment.display_name,
         ssphp.assessment.remediationDescription as ssphp.assessment.remediation_description,
         ssphp.assessment.status.firstEvaluationDate as ssphp.assessment.firstEvaluationDate,
         ssphp.assessment.status.statusChangeDate as ssphp.assessment.statusChangeDate
         *{} as *

`ssphp_use_case_write{{environment}}`

"""
