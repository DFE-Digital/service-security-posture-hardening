[ssphp_use_case_defender_assessments{{environment}}]
cron_schedule = 30 3 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments" earliest=-2d@d latest=now

``` ##################### replace with dashboard filter ##################### ```

    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments" earliest=-2d@d latest=now
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| spath SSPHP_RUN
| spath SSPHP_UID
| spath id
| spath name
| spath properties.description
| spath properties.displayName
| spath properties.metadata.assessmentType
| spath properties.metadata.categories{}
| spath properties.metadata.cloudProviders{}
| spath properties.metadata.createdBy
| spath properties.metadata.createdDateTimeUtc
| spath properties.metadata.description
| spath properties.metadata.displayName
| spath properties.metadata.implementationEffort
| spath properties.metadata.lastUpdatedBy
| spath properties.metadata.lastUpdatedDateTimeUtc
| spath properties.metadata.managementProvider
| spath properties.metadata.model.filterLocale.value
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.displayCache.name
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.displayCache.value
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.filteredPartIds{}
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.model.format
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.model.granularity
| spath properties.metadata.model.filters.value.MsPortalFx_TimeRange.model.relative
| spath properties.metadata.model.timeRange.type
| spath properties.metadata.model.timeRange.value.relative.duration
| spath properties.metadata.model.timeRange.value.relative.timeUnit
| spath properties.metadata.policyDefinitionId
| spath properties.metadata.preview
| spath properties.metadata.publishDates.ga
| spath properties.metadata.publishDates.public
| spath properties.metadata.remediationDescription
| spath properties.metadata.remediationPolicy
| spath properties.metadata.securityIssue
| spath properties.metadata.severity
| spath properties.metadata.tactics{}
| spath properties.metadata.techniques{}
| spath properties.metadata.threats{}
| spath properties.metadata.userImpact
| spath properties.resourceDetails.Id
| spath properties.resourceDetails.MachineName
| spath properties.resourceDetails.NativeResourceId
| spath properties.resourceDetails.ResourceId
| spath properties.resourceDetails.ResourceName
| spath properties.resourceDetails.ResourceProvider
| spath properties.resourceDetails.ResourceType
| spath properties.resourceDetails.Source
| spath properties.resourceDetails.SourceComputerId
| spath properties.resourceDetails.VMUUID
| spath properties.resourceDetails.WorkspaceId
| spath properties.resourceDetails.source
| spath properties.status.cause
| spath properties.status.code
| spath properties.status.description
| spath properties.status.firstEvaluationDate
| spath properties.status.severity
| spath properties.status.statusChangeDate
| spath resourceGroup
| spath subscriptionId
| spath tenantId
| spath type

| search NOT resourceGroup=""

| eval dashboard_resource_id=mvdedup(mvappend('properties.resourceDetails.NativeResourceId', 'properties.resourceDetails.id', 'properties.resourceDetails.ResourceId')),
       dashboard_properties_metadata=mvdedup(mvappend('properties.displayName', 'properties.description', 'properties.metadata.displayName')),
       resource_id=lower(coalesce('properties.resourceDetails.Id','properties.resourceDetails.id')),
       display_name=coalesce('properties.displayName','properties.metadata.displayName'),
       assessment_description=coalesce('properties.description','properties.metadata.description'),
       ssphp.resource.type=mvindex(split('resource_id',"/"),mvcount(split('resource_id',"/"))-3)."/".mvindex(split('resource_id',"/"),mvcount(split('resource_id',"/"))-2)

| where isnotnull('resource_id')

| rename *{}* as ** | rename *{}* as ** | rename *{}* as **

``` vvvvvvvvvvvvvvvvvv START : ADD SERVICE METADATA vvvvvvvvvvvvvvvvvv ```

``` `ssphp_add_service_metadata{{environment}}(resource_id)` ```
| lookup ssphp_bdmc.csv resource_id local=true
              OUTPUT product_key, subscription_name
              
| lookup ssphp_bdmc_fbp.csv product_key local=true
              OUTPUT portfolio as ssphp.service.portfolio,
                     service_line as ssphp.service.service_line,
                     product as ssphp.service.product
                     id as ssphp.resource.id,
                     cost_centre_code as ssphp.service.cost_centre.code,
                     account_code as ssphp.service.cost_centre.account_code,
                     activity_code as ssphp.service.cost_centre.activity_code,
                     cost_centre_title as ssphp.service.cost_centre.title,
                     cost_centre_owner as ssphp.service.contacts.cost_centre_owner,
                     financial_business_partner_email as ssphp.service.contacts.financial_business_partner_email,
                     hosting_provider_email as ssphp.service.contacts.hosting_provider_email,
                     hosting_support_email as ssphp.service.contacts.hosting_support_email,
                     product_owner_email as ssphp.service.contacts.product_owner_email
                    
| eval ssphp.service.portfolio=if(isnull('ssphp.service.portfolio'),"Unassigned",'ssphp.service.portfolio'),
       ssphp.service.service_line=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.service_line'),
       ssphp.service.product=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.product'),
       ssphp.service.cost_centre.code=if('ssphp.service.portfolio'="Unassigned","Unassigned",'ssphp.service.cost_centre.code')

| eval ssphp.service.id='ssphp.service.cost_centre.code'

``` ^^^^^^^^^^^^^^^^^ END : ADD SERVICE METADATA ^^^^^^^^^^^^^^^^^ ```

| rename properties.metadata.* as ssphp.assessment.*,
         properties.status.* as ssphp.assessment.status.*,
         display_name as ssphp.assessment.display_name,
         assessment_description as ssphp.assessment.description,
         
         properties.resourceDetails.* as ssphp.resource.*,
         
         id as ssphp.assessment.id,
         name as ssphp.assessment.name,
         type as ssphp.assessment.type,
         
         resource_id as ssphp.resource.id,
         tenantId as ssphp.resource.tenant, 
         subscriptionId as ssphp.resource.subscription,
         subscription_name as ssphp.resource.subscription_name,
         resourceGroup as ssphp.resource.resource_group

| fields - ssphp.assessment.model*, ssphp.assessment.displayName
| fields ssphp.assessment.*, ssphp.service.*, ssphp.resource.*


``` ##################### end dashboard query ##################### ```

| rex field=ssphp.assessment.id "[\s\S]*\/(?<ssphp_use_case_id>[\s\S]*)$"
| rename ssphp_use_case_id as ssphp.use_case.id

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id='ssphp.assessment.name',
       ssphp.use_case.title="Microsoft Defender Assessment [".'ssphp.assessment.name'."]",
       ssphp.use_case.savedsearch="ssphp_use_case_defender_assessments{{environment}}",
       ssphp.use_case.group="AZURE",
       ssphp.use_case.group_name="MS-DEFENDER",
       ssphp.use_case.version.number="5.0.1",
       ssphp.use_case.version.last_changed_date="2024-10-11",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.category="POSTURE",
       ssphp.assessment.source="Defender"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.ciso_priority=case(lower('ssphp.assessment.severity')="high","1",
                                      lower('ssphp.assessment.severity')="medium","2",
                                      1==1,"3"),
       ssphp.score.scoring_narrative="In order to be compliant, the value of properties.status.code MUST be 'Healthy' for each of the Resources listed.
Note : the health status relates only to the Assessment checked for this specific Control : 'xxxxxxxxxx'.
All the resources marked as 'Unhealthy' must be remediated"

| fields SSPHP_RUN, ssphp.*


| eval ssphp.score.numerator=coalesce(mvcount(mvfilter(match('ssphp.assessment.status.code',"Unhealthy"))),"0"),
       ssphp.score.denominator=coalesce(mvcount('ssphp.assessment.status.code'),"0"),
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator'),
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

| eval SSPHP_UID=md5('ssphp.use_case.category'."|".'ssphp.service.id'."|".'ssphp.use_case.id'."|".'ssphp.resource.id')

| eval ssphp.score.color=case('ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="1","red",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="2","orange",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="3","white",
                              1==1,'ssphp.score.color')

| table SSPHP_RUN, SSPHP_UID, ssphp.*


`ssphp_use_case_write{{environment}}`

"""


