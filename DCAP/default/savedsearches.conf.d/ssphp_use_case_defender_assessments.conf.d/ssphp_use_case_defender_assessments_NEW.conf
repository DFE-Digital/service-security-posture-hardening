[ssphp_use_case_defender_assessments_NEW{{environment}}]
cron_schedule = 30 3 * * *
dispatch.earliest_time = 0
dispatch.latest_time = now
enableSched = 1
search = """

`ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments" earliest=-2d@d latest=now

``` ##################### replace with dashboard filter ##################### ```

    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="azure_resource_graph" type="microsoft.security/assessments" earliest=-2d@d latest=now
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| search NOT resourceGroup=""


| eval dashboard_resource_id=mvdedup(mvappend('properties.resourceDetails.NativeResourceId', 'properties.resourceDetails.id', 'properties.resourceDetails.ResourceId')),
       dashboard_properties_metadata=mvdedup(mvappend('properties.displayName', 'properties.description', 'properties.metadata.displayName')),
       resource_id=lower(coalesce('properties.resourceDetails.Id','properties.resourceDetails.id')),
       display_name=coalesce('properties.displayName','properties.metadata.displayName'),
       assessment_description=coalesce('properties.description','properties.metadata.description')

| lookup bdmc.csv tenantId, subscriptionId, resource_id OUTPUT portfolio as ssphp.service.portfolio,
                                                               service_line as ssphp.service.service_line,
                                                               product as ssphp.service.product,
                                                               environment as ssphp.service.environment,
                                                               cost_centre_code as ssphp.service.cost_centre.code,
                                                               cost_centre_owner as ssphp.service.cost_centre.owner,
                                                               cost_centre_title as ssphp.service.cost_centre.title,
                                                               account_code as ssphp.service.cost_centre.account_code,
                                                               activity_code as ssphp.service.cost_centre.activity_code,
                                                               financial_business_partner_email as ssphp.service.contacts.financial_business_partner,
                                                               hosting_provider_email as ssphp.service.contacts.hosting_provider,
                                                               hosting_support_email as ssphp.service.contacts.hosting_support,
                                                               product_owner_email as ssphp.service.contacts.product_owner

| eval ssphp.service.id='ssphp.service.cost_centre.code'

| where isnotnull('ssphp.service.product')



| rename properties.resourceDetails.Id as remove1, properties.resourceDetails.id as remove2, properties.displayName as remove3, properties.metadata.displayName as remove4, properties.description as remove5, properties.metadata.description as remove6
```did this as a rename cos fields - will screw up dashboard search when it gets changed to table```

| rename properties.metadata.* as ssphp.assessment.*,
         properties.status.* as ssphp.assessment.status.*,
         display_name as ssphp.assessment.display_name,
         assessment_description as ssphp.assessment.description,
         
         properties.resourceDetails.* as ssphp.resource.*,
         
         id as ssphp.assessment.id,
         name as ssphp.assessment.name,
         type as ssphp.assessment.type,
         
         resource_id as ssphp.resource.id,
         tenantId as ssphp.resource.tenant, 
         subscriptionId as ssphp.resource.subscription, 
         resourceGroup as ssphp.resource.resource_group

| fields ssphp.assessment.*, ssphp.service.*, ssphp.resource.*


``` ##################### end dashboard query ##################### ```

| rex field=ssphp.assessment.id "[\s\S]*\/(?<ssphp_use_case_id>[\s\S]*)$"
| rename ssphp_use_case_id as ssphp.use_case.id

| eval SSPHP_RUN=round(now())

| eval ssphp.use_case.id='ssphp.assessment.name',
       ssphp.use_case.title="Microsoft Defender Assessment [".'ssphp.assessment.name'."]",
       ssphp.use_case.savedsearch="ssphp_use_case_defender_assessments_NEW{{environment}}",
       ssphp.use_case.group="AZURE",
       ssphp.use_case.group_name="MS-DEFENDER",
       ssphp.use_case.version.number="4.0.0",
       ssphp.use_case.version.last_changed_date="2024-09-26",
       ssphp.use_case.version.last_changed_by="Ian Pearl",
       ssphp.use_case.category="POSTURE",
       ssphp.assessment.source="Defender"

| eval ssphp.risk.expectancy="5",
       ssphp.risk.impact="5"

| eval ssphp.score.threshold.green="99",
       ssphp.score.threshold.orange="-",
       ssphp.score.threshold.red="-",
       ssphp.score.ciso_priority=case(lower('ssphp.assessment.severity')="high","1",
                                      lower('ssphp.assessment.severity')="medium","2",
                                      1==1,"3"),
       ssphp.score.scoring_narrative="In order to be compliant, the value of properties.status.code MUST be 'Healthy' for each of the Resources listed.
Note : the health status relates only to the Assessment checked for this specific Control : 'xxxxxxxxxx'.
All the resources marked as 'Unhealthy' must be remediated"


| fields SSPHP_RUN, ssphp.*


| eval ssphp.score.numerator=coalesce(mvcount(mvfilter(match('ssphp.assessment.status.code',"Unhealthy"))),"0"),
       ssphp.score.denominator=coalesce(mvcount('ssphp.assessment.status.code'),"0"),
       ssphp.score.score=floor(('ssphp.score.denominator'-'ssphp.score.numerator')*100/'ssphp.score.denominator'),
       ssphp.score.color=case('ssphp.score.score'>'ssphp.score.threshold.green',"green",1==1,"red")

| eval SSPHP_UID=md5('ssphp.use_case.category'."|".'ssphp.service.id'."|".'ssphp.use_case.id'."|".'ssphp.resource.id')



| eval ssphp.score.color=case('ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="1","red",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="2","orange",
                              'ssphp.score.color'="red" AND 'ssphp.score.ciso_priority'="3","white",
                              1==1,'ssphp.score.color')

```| eval SSPHP_UID=if(isnotnull('ssphp.use_case.category'),md5('ssphp.service.id'."|".'ssphp.use_case.id'."|".'ssphp.resource.id'),null())```



``` `ssphp_use_case_write{{environment}}` ```

| fields *
| makejson SSPHP_RUN, SSPHP_UID, ssphp.* output=temp_raw 
| eval _time=SSPHP_RUN
| rename temp_raw as _raw
| fields _time, _raw

"""
