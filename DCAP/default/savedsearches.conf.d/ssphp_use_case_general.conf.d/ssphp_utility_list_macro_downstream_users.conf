[ssphp_utility_list_macro_downstream_users{{environment}}]
enableSched = 0

search = """

| rest /servicesNS/-/-/admin/macros count=0 splunk_server=local ```ALL MACROS```
| search eai:acl.app="{{app}}{{environment}}"
| rename title as macro
| fields macro

| join type=outer macro max=0
    [| rest /servicesNS/-/-/saved/searches splunk_server=local
     | eval title="savedsearch :: ".title
     | search eai:acl.app="{{app}}{{environment}}"
     | fields title, search, eai:acl.app

     | append [| rest /servicesNS/-/-/data/ui/views splunk_server=local
               | eval title="dashboard :: ".title
               | rename eai:data as search
               | search eai:acl.app="{{app}}{{environment}}"
               | table title, search, eai:acl.app]
          
     | append [| rest /servicesNS/-/-/admin/macros splunk_server=local
               | eval title="macro :: ".'title'
               | rename definition as search
               | search eai:acl.app="{{app}}{{environment}}"
               | table title, search]
     
     | eval search=replace('search',"```[\s\S]*```","")
     | rex  max_match=0 field=search "`(?<macros>[^`]*)`"
     | eval macros=mvdedup('macros')

     | fields title, macros
     | search macros="*"

     | mvexpand macros
     | rex field=macros "[/s/S]*\((?<macros_params>[^\)]*)\)"
     | eval p_no=coalesce(mvcount(split('macros_params',",")),0),
            macro=replace('macros',"\([^\)]*\)","(".'p_no'.")")

     | dedup title, macro
     | table macro, title]
     
| table macro, title
| stats values(title) as title by macro

"""