[ssphp_use_case_ado_data{{environment}}]

definition = """

`ssphp_metrics_data_index_DEV` sourcetype="ADO" source="git_repo_policy_join"
    [| search `ssphp_metrics_data_index_DEV` sourcetype="ADO" source="git_repo_policy_join"
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| rename repoId as repo_id, projectId as project_id, organization as organization, policyId as policy_id
| fields organization, project_id, repo_id, policy_id

| join type=outer organization, project_id, repo_id
    [| search `ssphp_metrics_data_index_DEV` sourcetype="ADO" SSPHP_DEBUG.type="fn git_repository_list"
         [| search `ssphp_metrics_data_index_DEV` sourcetype="ADO" SSPHP_DEBUG.type="fn git_repository_list"
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
          
    | rename id as repo_id, 
             name as repo_name,
             defaultBranch as repo_default_branch,
             isDisabled as repo_isdisabled,
             isInMaintenance as repo_ismaintenance,
             project.* as project_*,
             SSPHP_DEBUG.organization as organization,
             SSPHP_DEBUG.tenant as tenant,
             url as repo_url
              
    | table tenant, organization, project_id, project_name, project_revision, project_state, project_url, project_visibility, repo_id, repo_name, repo_default_branch, repo_isdisabled, repo_ismaintenance, repo_url
    ]

| join type=outer organization, project_id, policy_id
    [| search `ssphp_metrics_data_index_DEV` sourcetype="ADO" SSPHP_DEBUG.type="fn policy_configuration_get"
         [| search `ssphp_metrics_data_index_DEV` sourcetype="ADO" SSPHP_DEBUG.type="fn policy_configuration_get"
          | stats max(SSPHP_RUN) as SSPHP_RUN
          | return SSPHP_RUN]
          
     | rename id as policy_id,
              createdBy.displayName as policy_created_by_display_name,
              createdDate as policy_created_date,
              isBlocking as policy_is_blocking,
              isDeleted as policy_is_deleted,
              isEnabled as policy_is_enabled,
              isEnterpriseManaged as policy_is_enterprise_managed,
              revision as policy_revision,
              
              settings.scope{}.* as policy_settings_scope_*,              
              settings.* as policy_settings_*,
              
              url as policy_url,
              type.id as policy_type_id,
              type.displayName as policy_type_display_name,
              type.url as policy_type_url,
              SSPHP_DEBUG.project as project_id,
              SSPHP_DEBUG.organization as organization
              
     | table organization, project_id, policy_id, policy_type_display_name, policy_created_by_display_name, policy_created_date, policy_is_blocking, policy_is_deleted, policy_is_enabled, policy_is_enterprise_managed, policy_revision, 
             policy_settings_*,
             policy_type_id, policy_url, policy_type_url
     ]

| lookup ssphp_ado_repo_owner.csv organization, project_id, repo_id OUTPUT portfolio as ssphp.service.portfolio, service_line as ssphp.service.service_line, product as ssphp.service.product
| fillnull value="Unassigned" ssphp.service.portfolio, ssphp.service.service_line, ssphp.service.product

| eval policy_branch_matches_repo_default=case(lower('policy_settings_scope_matchKind')="defaultbranch","true", 
                                               lower('policy_settings_scope_matchKind')="exact" AND lower('policy_settings_scope_refName')=lower('repo_default_branch'),"true", 
                                               lower('policy_settings_scope_matchKind')="prefix" AND like(lower('repo_policy_defaultBranch'),lower('policy_settings_scope_refName')."%"),"true", 
                                               1==1,"false")

| fields tenant, organization, project_id, project_name, project_revision, project_state, project_url, project_visibility, repo_id, repo_name, repo_default_branch, repo_isdisabled, repo_ismaintenance, repo_url, policy_id, 
         policy_type_id, policy_type_display_name, policy_created_by_display_name, policy_created_date, policy_is_blocking, policy_is_deleted, policy_is_enabled, policy_is_enterprise_managed, policy_revision, 
         policy_settings_*,
         policy_url, policy_type_url,
         policy_branch_matches_repo_default,
         ssphp.*

```====================================== MACRO ENDS HERE ======================================================================================```

"""
iseval = 0



