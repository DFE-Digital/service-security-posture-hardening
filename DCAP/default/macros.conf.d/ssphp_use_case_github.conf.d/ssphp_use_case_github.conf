[ssphp_use_case_github{{environment}}(1)]
definition = """
search `ssphp_metrics_data_index{{environment}}` sourcetype="github" source="$target_field$"
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="github" source="$target_field$"
     | stats max(SSPHP_RUN) as SSPHP_RUN
     | return SSPHP_RUN]

| rex field=source "\/repos(?<full_repo_name>\/[^\/]*\/[^\/]*)\/"

"""

args = target_field
iseval = 0




[ssphp_use_case_github{{environment}}(2)]
definition = """

search `ssphp_metrics_data_index{{environment}}` sourcetype="github" (source="$target_field1$" OR source="$target_field2$")
    [| search `ssphp_metrics_data_index{{environment}}` sourcetype="github" (source="$target_field1$" OR source="$target_field2$")
     | stats max(SSPHP_RUN) as SSPHP_RUN by source
     | eval search_text="source=\"".'source'."\" AND SSPHP_RUN=\"".'SSPHP_RUN'."\""
     | stats values(search_text) as search_text
     | eval search_text="(".mvjoin('search_text',") OR (").")"
     | return $search_text]

| rename required_pull_request_reviews.required_approving_review_count as required_approving_review_count,
         required_pull_request_reviews.require_last_push_approval as require_last_push_approval,
         required_pull_request_reviews.require_code_owner_reviews as require_code_owner_reviews

| eval reg_1="$target_field1$", reg_1=replace('reg_1',"\*","%"),
       reg_2="$target_field2$", reg_2=replace('reg_2',"\*","%"),
       http_status_1=if(like('source','reg_1'),'ssphp_http_status',""),
       http_status_2=if(like('source','reg_2'),'ssphp_http_status',"")

| rex field=source "\/repos(?<full_repo_name>\/[^\/]*\/[^\/]*)\/"
| rex field=contents_url "^.*repos(?<full_repo_name2>\/[^\/]*\/[^\/]*)\/.*$"

| fields full_repo_name, full_repo_name2, url, http_status_1, http_status_2, type, 
         required_approving_review_count, 
         require_last_push_approval,
         require_code_owner_reviews, 
         parameters.*
         
| eval repo_name=coalesce('full_repo_name', 'full_repo_name2', "-")
| fields - full_repo_name, full_repo_name2
| stats values(*) as * by repo_name

| fillnull value="-" type
| table repo_name, url, http_status_1, http_status_2, type, 
         required_approving_review_count, 
         require_last_push_approval,
         require_code_owner_reviews, 
         parameters.*


"""

args = target_field1,target_field2
iseval = 0
